sinh3ck

high

# DoS Of Any Pool By Transferring Loan Tokens To Pool

## Summary

Deployed pools can be DoS'ed before the first deposit by transferring loan tokens to pool contract.

## Vulnerability Detail

The function `deposit()` makes is intended to be used to deposit loan tokens into the pool in exchange for pool tokens. This function makes use of another function named `tokenToShares()` to calculate the amount to pool tokens the user will receive for the amount of loan tokens deposited.

When a new pool is created, all three variables `_currentTotalDebt`, `_loanTokenBalance`, and `_currentTotalSupply` contain 0. When the function `tokenToShares()` is called with a positive `amount` and these variables, the first thing to be verified is if `_currentTotalDebt` plus `_loanTokenBalance` (`_supplied`) equals to 0, and returns `_tokenAmount` immediately. This is meant to facilitate the first deposit into the pool.

However, it is possible to bypass this "if" statement ant still get an amount of `_shares` equal to 0. A malicious user may transfer loan tokens to the pool without using the function `deposit()`. In this case, the variable `_loanTokenBalance` will not start as 0 and the "if" statement inside `tokenToShares()` is bypassed while still returning 0 shares (due to the multiplication). After that, the "require" statement on `deposit()` will always revert and no legitimate deposits can be made.

The following PoC can be used to exploit this issue.

```solidity
function testDepositExploit() external {
    uint amount = 1e18;
    MockERC20 collateralToken = new MockERC20(0, 18);
    MockERC20 loanToken = new MockERC20(2 * amount, 18);
    Pool pool = factory.deploySurgePool(IERC20(address(collateralToken)), IERC20(address(loanToken)), 1e18, 0.8e18, 1e15, 1e15, 0.1e18, 0.4e18, 0.6e18);
    loanToken.approve(address(pool), amount);
    loanToken.transfer(address(pool), 1); // this line exploits the pool contract
    pool.deposit(amount);
}
```

## Impact

A malicious user may simply transfer 1 loan token to any pool contract right after its generation and before the first deposit. All further deposits will always revert. This is possible on every deployed pool.

## Code Snippet

[Pool.sol-L307](https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L307)
```solidity
function deposit(uint amount) external {
    uint _loanTokenBalance = LOAN_TOKEN.balanceOf(address(this));
    (address _feeRecipient, uint _feeMantissa) = FACTORY.getFee();
    (  
        uint _currentTotalSupply,
        uint _accruedFeeShares,
        uint _currentCollateralRatioMantissa,
        uint _currentTotalDebt
    ) = getCurrentState(
        _loanTokenBalance,
        _feeMantissa,
        lastCollateralRatioMantissa,
        totalSupply,
        lastAccrueInterestTime,
        lastTotalDebt
    );

    uint _shares = tokenToShares(amount, (_currentTotalDebt + _loanTokenBalance), _currentTotalSupply, false);
    require(_shares > 0, "Pool: 0 shares");
    _currentTotalSupply += _shares;

    // commit current state
    balanceOf[msg.sender] += _shares;
    totalSupply = _currentTotalSupply;
    lastTotalDebt = _currentTotalDebt;
    lastAccrueInterestTime = block.timestamp;
    lastCollateralRatioMantissa = _currentCollateralRatioMantissa;
    emit Invest(msg.sender, amount);
    emit Transfer(address(0), msg.sender, _shares);
    if(_accruedFeeShares > 0) {
        balanceOf[_feeRecipient] += _accruedFeeShares;
        emit Transfer(address(0), _feeRecipient, _accruedFeeShares);
    }

    // interactions
    safeTransferFrom(LOAN_TOKEN, msg.sender, address(this), amount);
}
```

[Pool.sol-L199](https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L199)
```solidity
function tokenToShares (uint _tokenAmount, uint _supplied, uint _sharesTotalSupply, bool roundUpCheck) internal pure returns (uint) {
    if(_supplied == 0) return _tokenAmount;
    uint shares = _tokenAmount * _sharesTotalSupply / _supplied;
    if(roundUpCheck && shares * _supplied < _tokenAmount * _sharesTotalSupply) shares++;
    return shares;
}
```

## Tool used

Manual Review

## Recommendation

It is recommended to implement an additional check inside `tokenToShares()` to guarantee that the calculation of the shares takes into consideration the fact that if `_supplied` is positive, then` _sharesTotalSupply` should also be positive.