unforgiven

high

# Attacker can manipulate loan token price per share in the Pool and users would lose funds due to division error

## Summary
Function `deposit()` is used for deposit loan tokens in exchange for pool tokens and function `withdraw()` is used for withdraw loan tokens in exchange for pool tokens. to convert loan token amount to share token code uses function `tokenToShares()`. when contract has low loan token supply (for example in early stage), attacker can send loan token directly to the contract address and it would cause contract state to have higher loan token per share price and users would lose funds due to division error when code calculates share amount in the `deposit()` and `withdraw()` function.

## Vulnerability Detail
This is `deposit()` function:
```solidity
   function deposit(uint amount) external {
        uint _loanTokenBalance = LOAN_TOKEN.balanceOf(address(this));
        (address _feeRecipient, uint _feeMantissa) = FACTORY.getFee();
        (  
            uint _currentTotalSupply,
            uint _accruedFeeShares,
            uint _currentCollateralRatioMantissa,
            uint _currentTotalDebt
        ) = getCurrentState(
            _loanTokenBalance,
            _feeMantissa,
            lastCollateralRatioMantissa,
            totalSupply,
            lastAccrueInterestTime,
            lastTotalDebt
        );

        uint _shares = tokenToShares(amount, (_currentTotalDebt + _loanTokenBalance), _currentTotalSupply, false);
        require(_shares > 0, "Pool: 0 shares");
        _currentTotalSupply += _shares;

        // commit current state
        balanceOf[msg.sender] += _shares;
        totalSupply = _currentTotalSupply;
        lastTotalDebt = _currentTotalDebt;
        lastAccrueInterestTime = block.timestamp;
        lastCollateralRatioMantissa = _currentCollateralRatioMantissa;
        emit Invest(msg.sender, amount);
        emit Transfer(address(0), msg.sender, _shares);
        if(_accruedFeeShares > 0) {
            balanceOf[_feeRecipient] += _accruedFeeShares;
            emit Transfer(address(0), _feeRecipient, _accruedFeeShares);
        }

        // interactions
        safeTransferFrom(LOAN_TOKEN, msg.sender, address(this), amount);
    }
```
As you can see code gets current loan token balance of the contract and then send it to the `tokenToShare()` to calculate share amount. 
This is `tokenToShare()` code:
```solidity
    function tokenToShares (uint _tokenAmount, uint _supplied, uint _sharesTotalSupply, bool roundUpCheck) internal pure returns (uint) {
        if(_supplied == 0) return _tokenAmount;
        uint shares = _tokenAmount * _sharesTotalSupply / _supplied;
        if(roundUpCheck && shares * _supplied < _tokenAmount * _sharesTotalSupply) shares++;
        return shares;
    }
```
code calculates share amount based on ratio of the token amount to all the token supply. attacker can use this and perform this attack:
1. when Pool is created recently and has no loan token supply attacker would do:
2. attacker would call `deposit(1 wei)` and would supply 1 token and would receive 1 wei share.
3. then attacker would transfer `100e18 - 1`  loan token to the contract address and now loan token share supply would be 1 and loan token balance of the contract would be 100e18.
4. after a time User1 would call `deposit(150 * 1e18)` to deposit 150 loan token and receive share tokens.
5. code would calculate share amount as `amount * shareSupply / loanTokenSupply = 150 * 1e18 * 1 / (100 * 1e18) = 1`.
6. so User1 would receive 1 share and would pay `150 * 1e18` loan tokens. total share would be 2 in the contract and token loan token balance would be `250 * 1e18`.
7. now attacker can withdraw his 1 share and receive 50% of the contract balance which is `125 * 1e18` tokens.
8. User1 lost `25 * 1e18` token due to the division error because attacker inflated token per share price. and from now on any user interacting with contract by calling `deposit()` and `withdraw()` would lose up to `100 * 1e18` tokens. 
9. attacker can perform this attack when contract loan token supply is low.

## Impact
attacker can manipulate token per share price and users would lose funds when they deposit or withdraw loan tokens and attacker can steal their funds.

## Code Snippet
https://github.com/Surge-fi/surge-protocol-v1/blob/b7cb1dc2a2dcb4bf22c765a4222d7520843187c6/src/Pool.sol#L307-L326

https://github.com/Surge-fi/surge-protocol-v1/blob/b7cb1dc2a2dcb4bf22c765a4222d7520843187c6/src/Pool.sol#L348-L372

https://github.com/Surge-fi/surge-protocol-v1/blob/b7cb1dc2a2dcb4bf22c765a4222d7520843187c6/src/Pool.sol#L199-L204

## Tool used
Manual Review

## Recommendation
either add extra precision and decimal for share amount (by multiplying to 1e18) or keep track of the loan token supply in contract state and don't use `LOAN_TOKEN.balanceOf(this)`.