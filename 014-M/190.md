joestakey

medium

# A repayer repaying the entire debt of a borrower can be front-run by the borrower and repay more than expected

## Summary
Repaying the entire debt of a borrower is open to front-running by the borrower to increase their debt, which leads to the repayer repaying a higher amount that what they intended

## Vulnerability Detail
The `repay()` function allows to repay the debt of a `borrower`, at an `amount` specified as a function input.
Because interest is accrued over time continuously, it is not possible to predict the exact amount of debt a `borrower` will have upon repaying (as it will depend on other transactions that may happen in the same block beforehand).
The function `repay()` allows the repayer to specify `amount == type(uint256).max` if they wish to repay the entire debt.

```solidity
File: src/Pool.sol
524: if(amount == type(uint).max) { //@audit M: repay FR ->
525:             amount = getDebtOf(debtSharesBalanceOf[borrower], _debtSharesSupply, _currentTotalDebt);
526:             _shares = debtSharesBalanceOf[borrower];
527:         } else {
528:             _shares = tokenToShares(amount, _currentTotalDebt, _debtSharesSupply, false);
529:         }
530:         _current
```

The issue is that it opens the repayer to a front-run attack from the borrower: if the borrower front-runs a `repay(borrower, type(uint256).max)` call with a `borrow` call to increase their debt, the `repay()` call will also repay that additional debt.

E.g:
- Bob borrows `1e5 USDC`
- Alice agrees to repay Bob's debt (`1e5` + interests), and calls `repay(Bob, type(uint256).max)`
- Bob front-runs that call and borrows an additional `1e5 USDC`.
- Alice will have repaid `2e5 USDC` (+ interests).

Add this test to `Pool.t.sol`:

```solidity
    function testAuditRepayFR() external {
        MockERC20 collateralToken = new MockERC20(1000e18, 18);
        MockERC20 loanToken = new MockERC20(1000e18, 18);
        address Alice = address(1);
        loanToken.mint(Alice, 1000e18);

        Pool pool = factory.deploySurgePool(IERC20(address(collateralToken)), IERC20(address(loanToken)), 1e18, 0.8e18, 1e15, 1e15, 0.1e18, 0.4e18, 0.6e18);

        loanToken.approve(address(pool), 1000e18);
        collateralToken.approve(address(pool), 1000e18);
        pool.deposit(100e18);
        pool.addCollateral(address(this), 100e18);
        pool.borrow(20e18);
        vm.warp(block.timestamp + 365 days);

        uint256 balanceAliceBefore = loanToken.balanceOf(Alice);
        // Alice repays the entire debt, but we front run the call to borrow more
        pool.borrow(20e18);
        vm.startPrank(Alice);
        loanToken.approve(address(pool), type(uint256).max);
        pool.repay(address(this), type(uint256).max);
        vm.stopPrank();
        uint256 balanceAliceAfter = loanToken.balanceOf(Alice);
        //debt is fully repaid as expected
        assertEq(pool.lastTotalDebt(), 0);
        //but Alice repaid more than expected
        assertApproxEqRel(balanceAliceBefore - balanceAliceAfter, 43.5e18, 1e16); //1% delta
    }
```
Note that you also need to add the following function to `MockERC20.sol`

```solidity
function mint(address to, uint amount) external {
        balanceOf[to] += amount;
}
```

## Impact
This puts a risk of theft on every user repaying a full debt on behalf of a borrower, which limits the composability of Surge.

## Code Snippet
https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L524-L529

## Tool used
Manual Review, Foundry

## Recommendation
To prevent the repayer from paying more than expected, the `amount` should act as an upper boundary: if the debt is higher, `amount` will be used.


```diff
File: src/Pool.sol
-524:        if(amount == type(uint).max) {
-525:             amount = getDebtOf(debtSharesBalanceOf[borrower], _debtSharesSupply, _currentTotalDebt);
-526:             _shares = debtSharesBalanceOf[borrower];
-527:         } else {
+               amount = Math.min(getDebtOf(debtSharesBalanceOf[borrower], _debtSharesSupply, _currentTotalDebt), amount);
528:             _shares = tokenToShares(amount, _currentTotalDebt, _debtSharesSupply, false);
-529:         }
```

This complexifies the repaying process for external protocols/users, as it means they should be able to query the debt of a specific borrower to be able to estimate how much they should repay.

You should consider creating a public version of `getDebtOf()`, that uses `getCurrentState()` to compute the exact debt of a user at a specific timestamp.

This will allow external protocols to compute a good approximation of the actual debt of a user.

A repaying process could then look like this:

```solidity
    uint256 amountUser = getDebtOfPublic(user); //@audit view function to retrieve debt of a user including interests
    pool.repay(user, amountUser + _slippage); //@audit _slippage would allow to specify the additional interest that may be accrued in the block before the call to the function
    require(pool.debtSharesBalanceOf(user) == 0, "debt not fully repaid"); //@audit additional check to ensure debt fully repaid, in case the debt was increased by the borrower.
```