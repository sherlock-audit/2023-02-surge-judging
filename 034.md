ABA

high

# Protocol can be DoS by dusting it before any deposits were made.

## Summary

Before any deposits are made, if an attacker sends any amount of load token to the contract, then any subsequent deposit will revert/fail.

## Vulnerability Detail

In `Pool.sol` when a deposit is being made, it initially collects information about the current state of the system:
[code](https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L307-L322)
```Solidity
    function deposit(uint amount) external {
        uint _loanTokenBalance = LOAN_TOKEN.balanceOf(address(this));
        (address _feeRecipient, uint _feeMantissa) = FACTORY.getFee();
        (  
            uint _currentTotalSupply,
            uint _accruedFeeShares,
            uint _currentCollateralRatioMantissa,
            uint _currentTotalDebt
        ) = getCurrentState(
            _loanTokenBalance,
            _feeMantissa,
            lastCollateralRatioMantissa,
            totalSupply,
            lastAccrueInterestTime,
            lastTotalDebt
        );
```
- among others, the current loan token balance is collected via: `balanceOf`

[code](https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L308)
```Solidity
        uint _loanTokenBalance = LOAN_TOKEN.balanceOf(address(this));
```
- this value, along with amount to deposit, current total debt and supply, are used to determine the amount of shares the depositor will receive via the function `tokenToShares`
```Solidity
        uint _shares = tokenToShares(amount, (_currentTotalDebt + _loanTokenBalance), _currentTotalSupply, false);
```

The issue arise in the way `tokenToShares` is called and how it is implemented:
[code](https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L199-L204)
```Solidity
    function tokenToShares (uint _tokenAmount, uint _supplied, uint _sharesTotalSupply, bool roundUpCheck) internal pure returns (uint) {
        if(_supplied == 0) return _tokenAmount;
        uint shares = _tokenAmount * _sharesTotalSupply / _supplied;
        if(roundUpCheck && shares * _supplied < _tokenAmount * _sharesTotalSupply) shares++;
        return shares;
    }
```
For the very first deposit in the Pool, the `_supplied` argument, which is `_currentTotalDebt + _loanTokenBalance` should be 0 and trigger the first if, resulting in `_tokenAmount` being used as share value.
[code](https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L200)
```Solidity
        if(_supplied == 0) return _tokenAmount;
```

If dusted, say with 1 wei of loan token, then this verification is passed. Shares then becomes 0 on the next line (`_sharesTotalSupply` is 0, as this is the first ever deposit)
[code](https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L201)
```Solidity
        uint shares = _tokenAmount * _sharesTotalSupply / _supplied;
```
No further rounding is done, as `tokenToShares` is called in `deposit` with _false_ for `roundUpCheck` and also `_sharesTotalSupply` is 0. Thus `shares` is returned as 0 value
[code](https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L202-L203)
```Solidity
        if(roundUpCheck && shares * _supplied < _tokenAmount * _sharesTotalSupply) shares++;
        return shares;
```
Coming back in `deposit` the exact next line checks that shares should be greater then 0, and reverts on failure.
[code](https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L324-L325)
```Solidity
        uint _shares = tokenToShares(amount, (_currentTotalDebt + _loanTokenBalance), _currentTotalSupply, false);
        require(_shares > 0, "Pool: 0 shares");
```

Following the above flow, we can see that any dust value sent before an initial deposit will cause all subsequent deposits to fail.

## Impact

Any dust value sent before an initial deposit will cause all subsequent deposits to fail. 
A malicious actor ca watch for pools to be deployed (the `PoolDeployed` event) and dust them all with 1 wei of loan tokens.
No deposits can go through at this point, effectively DOS the Pool.

As an observation, as it is `withdraw` can be called to get the dusted tokens (because of the same issue with how `tokenToShares` calculates shares) but this operation can simply be frontrun (or backrun) by an attacker that again, deposits dust.

## Code Snippet

The following POC illustrates how deposit would revert. It can be added in `Pool.t.sol`
```Solidity
    function testDoSProtocolByDusting() external {
        uint amount = 1e36 + 1;
        MockERC20 collateralToken = new MockERC20(0, 18);
        MockERC20 loanToken = new MockERC20(amount, 18);
        Pool pool = factory.deploySurgePool(IERC20(address(collateralToken)), IERC20(address(loanToken)), 1e18, 0.8e18, 1e15, 1e15, 0.1e18, 0.4e18, 0.6e18);
        loanToken.approve(address(pool), amount);

        loanToken.transfer(address(pool), 1);
        assertEq(loanToken.balanceOf(address(pool)), 1);

        vm.expectRevert();
        pool.deposit(amount);
    }
```

## Tool used

Manual Review

## Recommendation

Create an internal accounting for loan token balance also (`_loanTokenBalance`). Do not take it from the current contract address by using `balanceOf`.