ctf_sec

high

# Liquidation may not make account healthier

## Summary

Adding Collateral leads to liquidation

## Vulnerability Detail

In the current implementation, the liquidation logic is implemented below:

```solidity
  /// @notice Seize collateral from an underwater borrower in exchange for repaying their debt
  /// @param borrower The address of the borrower to liquidate
  /// @param amount The amount of debt to repay
  /// @dev If amount is max uint, all debt will be liquidated
  function liquidate(address borrower, uint amount) external {
      uint _loanTokenBalance = LOAN_TOKEN.balanceOf(address(this));
      (address _feeRecipient, uint _feeMantissa) = FACTORY.getFee();
      (  
          uint _currentTotalSupply,
          uint _accruedFeeShares,
          uint _currentCollateralRatioMantissa,
          uint _currentTotalDebt
      ) = getCurrentState(
          _loanTokenBalance,
          _feeMantissa,
          lastCollateralRatioMantissa,
          totalSupply,
          lastAccrueInterestTime,
          lastTotalDebt
      );

      uint collateralBalance = collateralBalanceOf[borrower];
      uint _debtSharesSupply = debtSharesSupply;
      uint userDebt = getDebtOf(debtSharesBalanceOf[borrower], _debtSharesSupply, _currentTotalDebt);
      uint userCollateralRatioMantissa = userDebt * 1e18 / collateralBalance;
      require(userCollateralRatioMantissa > _currentCollateralRatioMantissa, "Pool: borrower not liquidatable");

      address _borrower = borrower; // avoid stack too deep
      uint _amount = amount; // avoid stack too deep
      uint _shares;
      uint collateralReward;
      if(_amount == type(uint).max || _amount == userDebt) {
          collateralReward = collateralBalance;
          _shares = debtSharesBalanceOf[_borrower];
          _amount = userDebt;
      } else {
          uint userInvertedCollateralRatioMantissa = collateralBalance * 1e18 / userDebt;
          collateralReward = _amount * userInvertedCollateralRatioMantissa / 1e18; // rounds down
          _shares = tokenToShares(_amount, _currentTotalDebt, _debtSharesSupply, false);
      }
      _currentTotalDebt -= _amount;

      // commit current state
      debtSharesBalanceOf[_borrower] -= _shares;
      debtSharesSupply = _debtSharesSupply - _shares;
      collateralBalanceOf[_borrower] = collateralBalance - collateralReward;
      totalSupply = _currentTotalSupply;
      lastTotalDebt = _currentTotalDebt;
      lastAccrueInterestTime = block.timestamp;
      lastCollateralRatioMantissa = _currentCollateralRatioMantissa;
      emit Liquidate(_borrower, _amount, collateralReward);
      if(_accruedFeeShares > 0) {
          address __feeRecipient = _feeRecipient; // avoid stack too deep
          balanceOf[__feeRecipient] += _accruedFeeShares;
          emit Transfer(address(0), __feeRecipient, _accruedFeeShares);
      }

      // interactions
      safeTransferFrom(LOAN_TOKEN, msg.sender, address(this), _amount);
      safeTransfer(COLLATERAL_TOKEN, msg.sender, collateralReward);
  }
```

note the code, to make the account healthy, debtSharesBalanceOf[borrower] can be removed, or collateralBalance can be increased.

```solidity
  uint collateralBalance = collateralBalanceOf[borrower];
  uint _debtSharesSupply = debtSharesSupply;
  uint userDebt = getDebtOf(debtSharesBalanceOf[borrower], _debtSharesSupply, _currentTotalDebt);
  uint userCollateralRatioMantissa = userDebt * 1e18 / collateralBalance;
  require(userCollateralRatioMantissa > _currentCollateralRatioMantissa, "Pool: borrower not liquidatable");
```

the key is to make sure the line of code revert, otherwise, liquidation happens.

```solidity
require(userCollateralRatioMantissa > _currentCollateralRatioMantissa, "Pool: borrower not liquidatable");
```

the issue is that, in the current implementation, liquidation pays the debt shares but also remove collateralBalance.

Again, if debt shares goes down, collateralBalance goes up or not change, the account is healthier.
but if debt shares goes down, collateralBalance goes down as well, the account can be less healthy, given the calculation of the collateralReward

```solidity
      uint collateralReward;
      if(_amount == type(uint).max || _amount == userDebt) {
          collateralReward = collateralBalance;
          _shares = debtSharesBalanceOf[_borrower];
          _amount = userDebt;
      } else {
          uint userInvertedCollateralRatioMantissa = collateralBalance * 1e18 / userDebt;
          collateralReward = _amount * userInvertedCollateralRatioMantissa / 1e18; // rounds down
          _shares = tokenToShares(_amount, _currentTotalDebt, _debtSharesSupply, false);
      }
      _currentTotalDebt -= _amount;

      // commit current state
      debtSharesBalanceOf[_borrower] -= _shares;
      debtSharesSupply = _debtSharesSupply - _shares;
      collateralBalanceOf[_borrower] = collateralBalance - collateralReward;
```

## Impact

If Liquidation may not make account healthier, series of liquidation can happens can user's collateral is lost before user has a chance to repay the debt.

## Code Snippet

https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L548-Ll609

## Tool used

Manual Review

## Recommendation

We recommend the protocol validates that the account is healthier after the liquidation by using the code to check again:

```solidity
  uint collateralBalance = collateralBalanceOf[borrower];
  uint _debtSharesSupply = debtSharesSupply;
  uint userDebt = getDebtOf(debtSharesBalanceOf[borrower], _debtSharesSupply, _currentTotalDebt);
  uint userCollateralRatioMantissa = userDebt * 1e18 / collateralBalance;
  require(userCollateralRatioMantissa < _currentCollateralRatioMantissa, "Pool: borrower still liquidable");
```
