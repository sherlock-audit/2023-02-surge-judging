bshramin

high

# Attacker can make a pool completely dysfunctional from the beginning

## Summary
From the get go, an attacker can make a pool completely dysfunctional. By sending any amount of the loanToken to the pool, the attacker can make the pool unable to receive any deposits, and the pool will be completely unusable.

## Vulnerability Detail

When depositing loanToken to the pool, to calculate the number of shares that the lender should receive, the `tokenToShares` function is called on line 324.

This is where it's called:
https://github.com/Surge-fi/surge-protocol-v1/blob/b7cb1dc2a2dcb4bf22c765a4222d7520843187c6/src/Pool.sol#L307
```solidity
function deposit(uint amount) external {
        uint _loanTokenBalance = LOAN_TOKEN.balanceOf(address(this));
        (address _feeRecipient, uint _feeMantissa) = FACTORY.getFee();
        (  
            uint _currentTotalSupply,
            uint _accruedFeeShares,
            uint _currentCollateralRatioMantissa,
            uint _currentTotalDebt
        ) = getCurrentState(
            _loanTokenBalance,
            _feeMantissa,
            lastCollateralRatioMantissa,
            totalSupply,
            lastAccrueInterestTime,
            lastTotalDebt
        );

        uint _shares = tokenToShares(amount, (_currentTotalDebt + _loanTokenBalance), _currentTotalSupply, false);
        require(_shares > 0, "Pool: 0 shares");
        _currentTotalSupply += _shares;

        // Goes on to update states and etc ... 
}

```
This is the function:
https://github.com/Surge-fi/surge-protocol-v1/blob/b7cb1dc2a2dcb4bf22c765a4222d7520843187c6/src/Pool.sol#L199
```solidity
function tokenToShares (uint _tokenAmount, uint _supplied, uint _sharesTotalSupply, bool roundUpCheck) internal pure returns (uint) {
        if(_supplied == 0) return _tokenAmount;
        uint shares = _tokenAmount * _sharesTotalSupply / _supplied;
        if(roundUpCheck && shares * _supplied < _tokenAmount * _sharesTotalSupply) shares++;
        return shares;
    }
```
At the beginning of the function, it checks if the `_supplied` is 0. If it is, it returns the `_tokenAmount`. This is the problem. The `_supplied` is the total amount of loanToken that has been deposited to the pool. If the attacker sends any amount of loanToken to the pool, the `_supplied` will be greater than 0, and the attacker can make the pool unusable. It will be unusable because `_supplied` will be greater than 0 while `_sharesTotalSupply` is still zero so
https://github.com/Surge-fi/surge-protocol-v1/blob/b7cb1dc2a2dcb4bf22c765a4222d7520843187c6/src/Pool.sol#L201
```solidity
uint shares = _tokenAmount * _sharesTotalSupply / _supplied;
```
will always result in zero for any amount of deposit and the pool will be unusable.


This is a serious problem because the attacker can see the creation transaction on mempool and since the address of the pool is known even before creation (from the formula below) the attacker can create and send the transfer transaction while the pool has not yet been created yet (attacker back-runs the creation transaction).
```solidity
address = rightmost_20_bytes(keccak(RLP(sender address, nonce)))
```

## Impact
The impact of this vulnerability is that the attacker can make any new pool completely unusable from the beginning.

## Code Snippet

This foundry test case shows the vulnerability in action:
```solidity
function testPreventDeposit() external {
    MockERC20 collateralToken = new MockERC20(0, 18);
    MockERC20 loanToken = new MockERC20(1000, 18);
    Pool pool = factory.deploySurgePool(IERC20(address(collateralToken)), IERC20(address(loanToken)), 1e18, 0.8e18, 1e15, 1e15, 0.1e18, 0.4e18, 0.6e18);

    loanToken.approve(address(pool), 100);
    loanToken.transfer(address(pool), 1);
    pool.deposit(100);

    assertEq(pool.balanceOf(address(this)), 100);
    assertEq(pool.totalSupply(), 100);
    pool.withdraw(100);
    assertEq(pool.balanceOf(address(this)), 0);
    assertEq(pool.totalSupply(), 0);
}
``` 
if we remove the line `loanToken.transfer(address(pool), 1);` the test will pass.


## Tool used

Manual Review

## Recommendation
My main recommendation is to not use `LOAN_TOKEN.balanceOf(address(this));` anywhere in the code. This is a very dangerous function to use because it can be manipulated by the attacker. Instead, keep a variable that keeps track of the total amount of loanToken that has been deposited to the pool. This variable should be updated every time a deposit or withdrawal happens. This way, the attacker cannot manipulate the `_supplied` variable.

For this particular vulnerability, I would recommend changing the check at the beginning of `tokenToShares` function to the following:

```solidity
if(_sharesTotalSupply == 0) return _tokenAmount;
```
It's a one-line change that can fix this vulnerability since the attacker cannot manipulate `_sharesTotalSupply`.
