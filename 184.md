Tricko

high

# No incentives to liquidate debt when remaining collateral is very small

## Summary
If a debt position has minimal collateral remaining, there is no incentive to liquidate it as the gas costs would negate any potential profit gained from the liquidation.

## Vulnerability Detail
In a liquidation event, the party responsible for liquidating a user's debt will pay off some or all of the debt and receive the user's collateral in return. However, the liquidation process only makes economic sense if the value of the collateral received by the liquidator is greater than the amount of debt repaid and the gas costs associated with the transaction. Therefore, there exists a minimum threshold value of collateral below which liquidating a debt position would never be profitable. These debt positions are considered bad debt and will accrue interest over time, making them even less favorable for liquidation. Unless the protocol's owners or other interested third-party spend their own funds to liquidate those bad debts, they will  keep increasing more and more and will become a bigger fraction of `_totalDebt`, therefore increasing utilization rate and possibly affecting the protocol's functionality in the long run.

Looking at the `liquidate` function internal logic (See code snippet below), we see that it allows arbitrary amounts of debt to be liquidated, possibly leading to bad debt if liquidators try to pay off amounts close to the total debt amount.

```javascript
function liquidate(address borrower, uint amount) external {
    uint _loanTokenBalance = LOAN_TOKEN.balanceOf(address(this));
    (address _feeRecipient, uint _feeMantissa) = FACTORY.getFee();
    (  
        uint _currentTotalSupply,
        uint _accruedFeeShares,
        uint _currentCollateralRatioMantissa,
        uint _currentTotalDebt
    ) = getCurrentState(
        _loanTokenBalance,
        _feeMantissa,
        lastCollateralRatioMantissa,
        totalSupply,
        lastAccrueInterestTime,
        lastTotalDebt
    );

    uint collateralBalance = collateralBalanceOf[borrower];
    uint _debtSharesSupply = debtSharesSupply;
    uint userDebt = getDebtOf(debtSharesBalanceOf[borrower], _debtSharesSupply, _currentTotalDebt);
    uint userCollateralRatioMantissa = userDebt * 1e18 / collateralBalance;
    require(userCollateralRatioMantissa > _currentCollateralRatioMantissa, "Pool: borrower not liquidatable");

    address _borrower = borrower; // avoid stack too deep
    uint _amount = amount; // avoid stack too deep
    uint _shares;
    uint collateralReward;
    if(_amount == type(uint).max || _amount == userDebt) {
        collateralReward = collateralBalance;
        _shares = debtSharesBalanceOf[_borrower];
        _amount = userDebt;
    } else {
        uint userInvertedCollateralRatioMantissa = collateralBalance * 1e18 / userDebt;
        collateralReward = _amount * userInvertedCollateralRatioMantissa / 1e18; // rounds down
        _shares = tokenToShares(_amount, _currentTotalDebt, _debtSharesSupply, false);
    }
    _currentTotalDebt -= _amount;

    // commit current state
    debtSharesBalanceOf[_borrower] -= _shares;
    debtSharesSupply = _debtSharesSupply - _shares;
    collateralBalanceOf[_borrower] = collateralBalance - collateralReward;
    totalSupply = _currentTotalSupply;
    lastTotalDebt = _currentTotalDebt;
    lastAccrueInterestTime = block.timestamp;
    lastCollateralRatioMantissa = _currentCollateralRatioMantissa;
    emit Liquidate(_borrower, _amount, collateralReward);
    if(_accruedFeeShares > 0) {
        address __feeRecipient = _feeRecipient; // avoid stack too deep
        balanceOf[__feeRecipient] += _accruedFeeShares;
        emit Transfer(address(0), __feeRecipient, _accruedFeeShares);
    }

    // interactions
    safeTransferFrom(LOAN_TOKEN, msg.sender, address(this), _amount);
    safeTransfer(COLLATERAL_TOKEN, msg.sender, collateralReward);
}
```
## Impact
If many positions fall into this state of bad debt, and if left unattended, the accumulation of interest on the bad debt could potentially impact the protocol's functionality in the long run.

## Code Snippet
https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L553-L609

## Tool used
Manual Review

## Recommendation
Consider restricting liquidators to two choices. Either the liquidator pays 100% of the debt or limit it to repay up to some value (e.g 90%). So there would be enough collateral left for a second profitable liquidations to happen.

