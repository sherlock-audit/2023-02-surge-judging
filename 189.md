Tricko

medium

# Mismatch between Pools' `decimals()` and the actual decimals of the pool token.

## Summary
If the loan token decimals are different than 18, `Pool` will return wrong decimals values.

## Vulnerability Detail
The README specifies that Pool contracts must conform to the ERC20 token standard. As a result, we can determine the number of decimals for a token by invoking the decimals() method, as expected from the ERC20 interface. For the `Pool` contract this value is hardcoded  (`uint8 public constant decimals = 18;`)

However the actual number of decimals utilized by the contract's logic is determined by the loan token's decimal value.  The `tokenToShares` function calculates the number of pool tokens (i.e., `shares`) that will be added to the user's balance. The code snippet provided below (extracted from the `deposit` function) shows that the `shares` value returned will match the number of decimals in the `amount`, `_currentTotalDebt`, `_loanTokenBalance` and `_currentTotalSupply` variables. All those variables are derived from the loan tokens amounts deposited, therefore will share loan token decimals.

```javascript
function tokenToShares (uint _tokenAmount, uint _supplied, uint _sharesTotalSupply, bool roundUpCheck) internal pure returns (uint) {
    if(_supplied == 0) return _tokenAmount;
    uint shares = _tokenAmount * _sharesTotalSupply / _supplied;
    if(roundUpCheck && shares * _supplied < _tokenAmount * _sharesTotalSupply) shares++;
    return shares;
}
```

```javascript
uint _shares = tokenToShares(amount, (_currentTotalDebt + _loanTokenBalance), _currentTotalSupply, false);
require(_shares > 0, "Pool: 0 shares");
_currentTotalSupply += _shares;

// commit current state
balanceOf[msg.sender] += _shares;
```

Therefore the ERC20's method `decimals()` will return incorrect number of decimals if `LOAN_TOKEN.decimals() != 18`. Possibly affecting integration of third-party contracts with the Surge protocol Pools.

## Impact
`Pool` funds are not directly affected by this. But integrations with external code may cause loss of funds to third-party contracts and consequently affecting Surge protocol reputation and future adoption.

## Code Snippet

https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L26

https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L307-L343

## Tool used
Manual Review

## Recommendation
Consider setting `decimal` value in the constructor, as shown in the diff below.

```diff
diff --git a/Pool.sol.orig b/Pool.sol
index 0fb9b27..bebdb94 100644
--- a/Pool.sol.orig
+++ b/Pool.sol
@@ -1,110 +1,111 @@
 // SPDX-License-Identifier: UNLICENSED
 pragma solidity 0.8.17;

 interface IERC20 {
     function balanceOf(address) external view returns(uint);
     function transferFrom(address sender, address recipient, uint256 amount) external returns(bool);
     function transfer(address, uint) external returns (bool);
     function decimals() external view returns (uint8);
 }

 interface IFactory {
     function getFee() external view returns (address to, uint feeMantissa);
 }

 /// @title Pool
 /// @author Moaz Mohsen & Nour Haridy
 /// @notice A Surge lending pool for a single collateral and loan token pair
 /// @dev This contract asssumes that the collateral and loan tokens are valid non-rebasing ERC20-compliant tokens
 contract Pool {

     IFactory public immutable FACTORY;
     IERC20 public immutable COLLATERAL_TOKEN;
     IERC20 public immutable LOAN_TOKEN;
     string public symbol;
     string public name;
-    uint8 public constant decimals = 18;
+    uint8 public decimals;
     uint private constant RATE_CEILING = 100e18; // 10,000% borrow APR
     uint public immutable MIN_RATE;
     uint public immutable SURGE_RATE;
     uint public immutable MAX_RATE;
     uint public immutable MAX_COLLATERAL_RATIO_MANTISSA;
     uint public immutable SURGE_MANTISSA;
     uint public immutable COLLATERAL_RATIO_FALL_DURATION;
     uint public immutable COLLATERAL_RATIO_RECOVERY_DURATION;
     bytes4 private constant TRANSFER_SELECTOR = bytes4(keccak256(bytes('transfer(address,uint256)')));
     bytes4 private constant TRANSFER_FROM_SELECTOR = bytes4(keccak256(bytes('transferFrom(address,address,uint256)')));
     uint public lastCollateralRatioMantissa;
     uint public debtSharesSupply;
     mapping (address => uint) public debtSharesBalanceOf;
     uint public lastTotalDebt;
     uint public lastAccrueInterestTime;
     uint public totalSupply;
     mapping (address => mapping (address => uint)) public allowance;
     mapping (address => uint) public balanceOf;
     mapping (address => uint) public collateralBalanceOf;

     constructor(
         string memory _symbol,
         string memory _name,
         IERC20 _collateralToken,
         IERC20 _loanToken,
         uint _maxCollateralRatioMantissa,
         uint _surgeMantissa,
         uint _collateralRatioFallDuration,
         uint _collateralRatioRecoveryDuration,
         uint _minRateMantissa,
         uint _surgeRateMantissa,
         uint _maxRateMantissa
     ) {
         require(_collateralToken != _loanToken, "Pool: collateral and loan tokens are the same");
         require(_collateralRatioFallDuration > 0, "Pool: _collateralRatioFallDuration too low");
         require(_collateralRatioRecoveryDuration > 0, "Pool: _collateralRatioRecoveryDuration too low");
         require(_maxCollateralRatioMantissa > 0, "Pool: _maxCollateralRatioMantissa too low");
         require(_surgeMantissa < 1e18, "Pool: _surgeMantissa too high");
         require(_minRateMantissa <= _surgeRateMantissa, "Pool: _minRateMantissa too high");
         require(_surgeRateMantissa <= _maxRateMantissa, "Pool: _surgeRateMantissa too high");
         require(_maxRateMantissa <= RATE_CEILING, "Pool: _maxRateMantissa too high");
         symbol = _symbol;
         name = _name;
         FACTORY = IFactory(msg.sender);
         COLLATERAL_TOKEN = _collateralToken;
         LOAN_TOKEN = _loanToken;
         MAX_COLLATERAL_RATIO_MANTISSA = _maxCollateralRatioMantissa;
         SURGE_MANTISSA = _surgeMantissa;
         COLLATERAL_RATIO_FALL_DURATION = _collateralRatioFallDuration;
         COLLATERAL_RATIO_RECOVERY_DURATION = _collateralRatioRecoveryDuration;
         lastCollateralRatioMantissa = _maxCollateralRatioMantissa;
         MIN_RATE = _minRateMantissa;
         SURGE_RATE = _surgeRateMantissa;
         MAX_RATE = _maxRateMantissa;
+        decimals = LOAN_TOKEN.decimals();
     }

     function safeTransfer(IERC20 token, address to, uint value) internal {
         (bool success, bytes memory data) = address(token).call(abi.encodeWithSelector(TRANSFER_SELECTOR, to, value));
         require(success && (data.length == 0 || abi.decode(data, (bool))), 'Pool: TRANSFER_FAILED');
     }

     function safeTransferFrom(IERC20 token, address from, address to, uint value) internal {
         (bool success, bytes memory data) = address(token).call(abi.encodeWithSelector(TRANSFER_FROM_SELECTOR, from, to, value));
         require(success && (data.length == 0 || abi.decode(data, (bool))), 'Pool: TRANSFER_FROM_FAILED');
     }

     /// @notice Gets the current state of pool variables based on the current time
     /// @param _loanTokenBalance The current balance of the loan token in the pool
     /// @param _feeMantissa The fee to be charged on interest accrual
     /// @param _lastCollateralRatioMantissa The collateral ratio at the last interest accrual
     /// @param _totalSupply The total supply of pool tokens at the last interest accrual
     /// @param _lastAccrueInterestTime The last time interest was accrued
     /// @param _totalDebt The total debt of the pool at the last interest accrual
     /// @return _currentTotalSupply The current total supply of pool tokens
     /// @return _accruedFeeShares The accrued fee shares to be transferred to the fee recipient
     /// @return _currentCollateralRatioMantissa The current collateral ratio
     /// @return _currentTotalDebt The current total debt of the pool
     /// @dev This view function behaves as a pure function with the exception of immutable variables (which are constant)
     function getCurrentState(
         uint _loanTokenBalance,
         uint _feeMantissa,
         uint _lastCollateralRatioMantissa,
         uint _totalSupply,
         uint _lastAccrueInterestTime,
```
