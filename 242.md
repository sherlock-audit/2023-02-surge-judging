joestakey

medium

# Borrowing max debt will result in liquidation in next block

## Summary
User taking up maximal debt can get liquidated in next block

## Vulnerability Detail
When borrowing, the function checks the debt does not exceed the maximum collateralization ratio:

```solidity
src/Pool.sol
474: uint userCollateralRatioMantissa = userDebt * 1e18 / collateralBalanceOf[msg.sender];
475:         require(userCollateralRatioMantissa <= _currentCollateralRatioMantissa, "Pool: user collateral ratio too high");
```

The issue is that the same collateralization ratio is used when checking if liquidation is possible:

```solidity
File: src/Pool.sol
573:         uint userCollateralRatioMantissa = userDebt * 1e18 / collateralBalance;
574:         require(userCollateralRatioMantissa > _currentCollateralRatioMantissa, "Pool: borrower not liquidatable");
```

Because of interest accrual, `userDebt` will increase as soon as the loan is taken.

This means a user borrowing the maximal debt can be liquidated in the following block.


Let us take the example of a pool on Polygon (block time 2s). For simplicity the `MAX_COLLATERAL_RATIO_MANTISSA` will be 100%:

- Bob `deposits` `200 token` in the pool.
- Alice calls `addCollateral`, adding `100 token` to the pool.
- Alice calls `borrow(Alice, 100 token)`.  
- `userCollateralRatioMantissa = userDebt * 1e18 / collateralBalanceOf[msg.sender]`
-                             `= 100e18 * 1e18 / 100e18`
-                             `= 1e18`
- This passes the collateralization check, and the function call succeeds.
- We wait 1 block (2s)
- Bob calls `liquidate(Alice, type(uin256).max)`.
- Because of interest accrual [here](https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L154-L156), `userDebt` has increased.
- `userCollateralRatioMantissa` will hence be greater than `1e18`, and pass the [liquidation check](https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L574). 

Add the following test to `Pool.t.sol`:
```Solidity
function testAuditLiquidateAfterBorrow() external {
    MockERC20 collateralToken = new MockERC20(1000e18, 18);
    MockERC20 loanToken = new MockERC20(1000e18, 18);
    address Alice = address(1);
    collateralToken.mint(Alice, 100e18);

    Pool pool = factory.deploySurgePool(IERC20(address(collateralToken)), IERC20(address(loanToken)), 1e18, 0.8e18, 1e15, 1e15, 0.1e18, 0.4e18, 0.6e18);
    
    // 1- We deposit 100 token in the pool
    loanToken.approve(address(pool), 200e18);
    pool.deposit(200e18);
    
    vm.startPrank(Alice);
    // 2- Alice adds 100 token as collateral
    collateralToken.approve(address(pool), 100e18);
    pool.addCollateral(Alice, 100e18); 

    // 3- Alice borrows 100 token
    uint256 balanceAliceBefore = loanToken.balanceOf(Alice);
    pool.borrow(100e18);
    vm.stopPrank();

    // 4 - wait 2s (1 block)
    vm.warp(2);

    // 4- We liquidate Alice
    loanToken.approve(address(pool), type(uint256).max);
    pool.liquidate(Alice, type(uint256).max);

    uint256 balanceCollateralAlice = pool.collateralBalanceOf(Alice);
    // Alice was liquidated 
    assertApproxEqRel(balanceCollateralAlice, 0, 1e16); //1% delta
}
```

Note that you also need to add the following function to `MockERC20.sol`

```solidity
function mint(address to, uint amount) external {
    balanceOf[to] += amount;
}
```

## Impact
There is essentially no gap between liquidation LTV and borrow LTV, leading to liquidation of max debt loan happening in next block.
Liquidations are harmful for both the protocol and the borrower being liquidated.

Note: this also happens because `lastCollateralRatioMantissa` is initialized at the maximum, so as long as utilization is less than or equal to surge, [this code block](https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L230-L232) will keep the `lastCollateralRatioMantissa` the same, leaving the max debt loans vulnerable to liquidation.

## Code Snippet
https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L573

## Tool used
Manual Review, Foundry

## Recommendation
Ensure the gap between liquidation LTV and borrow LTV is sufficient. 