unforgiven

high

# malicious borrower can manipulate utilization ratio and borrow rate and pay lower interest

## Summary
when users interact with the Pool contract, code updates the accrued interest and total debt of the borrowers, code uses this based on utilization ratio which calculates it based on contract current loan token balance. malicious borrower can use this and send loan token directly to contract address and lower the utilization ratio and lower the interest rate.

## Vulnerability Detail
This is `repay()` code:
```solidity
    function repay(address borrower, uint amount) external {
        uint _loanTokenBalance = LOAN_TOKEN.balanceOf(address(this));
        (address _feeRecipient, uint _feeMantissa) = FACTORY.getFee();
        (  
            uint _currentTotalSupply,
            uint _accruedFeeShares,
            uint _currentCollateralRatioMantissa,
            uint _currentTotalDebt
        ) = getCurrentState(
            _loanTokenBalance,
            _feeMantissa,
            lastCollateralRatioMantissa,
            totalSupply,
            lastAccrueInterestTime,
            lastTotalDebt
        );
        
        uint _debtSharesSupply = debtSharesSupply;

        uint _shares;
        if(amount == type(uint).max) {
            amount = getDebtOf(debtSharesBalanceOf[borrower], _debtSharesSupply, _currentTotalDebt);
            _shares = debtSharesBalanceOf[borrower];
        } else {
            _shares = tokenToShares(amount, _currentTotalDebt, _debtSharesSupply, false);
        }
        _currentTotalDebt -= amount;
.........
```
As you can see code gets the current contract loan token balance and send it to `getCurrentState()` function. 
This is `getCurrentState()` code:
```solidity
    function getCurrentState(
        uint _loanTokenBalance,
        uint _feeMantissa,
        uint _lastCollateralRatioMantissa,
        uint _totalSupply,
        uint _lastAccrueInterestTime,
        uint _totalDebt
        ) internal view returns (
            uint _currentTotalSupply,
            uint _accruedFeeShares,
            uint _currentCollateralRatioMantissa,
            uint _currentTotalDebt
        ) {
        
        // 1. Set default return values
        _currentTotalSupply = _totalSupply;
        _currentTotalDebt = _totalDebt;
        _currentCollateralRatioMantissa = _lastCollateralRatioMantissa;
        // _accruedFeeShares = 0;

        // 2. Get the time passed since the last interest accrual
        uint _timeDelta = block.timestamp - _lastAccrueInterestTime;
        
        // 3. If the time passed is 0, return the current values
        if(_timeDelta == 0) return (_currentTotalSupply, _accruedFeeShares, _currentCollateralRatioMantissa, _currentTotalDebt);
        
        // 4. Calculate the supplied value
        uint _supplied = _totalDebt + _loanTokenBalance;
        // 5. Calculate the utilization
        uint _util = getUtilizationMantissa(_totalDebt, _supplied);

        // 6. Calculate the collateral ratio
        _currentCollateralRatioMantissa = getCollateralRatioMantissa(
            _util,
            _lastAccrueInterestTime,
            block.timestamp,
            _lastCollateralRatioMantissa,
            COLLATERAL_RATIO_FALL_DURATION,
            COLLATERAL_RATIO_RECOVERY_DURATION,
            MAX_COLLATERAL_RATIO_MANTISSA,
            SURGE_MANTISSA
        );

        // 7. If there is no debt, return the current values
        if(_totalDebt == 0) return (_currentTotalSupply, _accruedFeeShares, _currentCollateralRatioMantissa, _currentTotalDebt);

        // 8. Calculate the borrow rate
        uint _borrowRate = getBorrowRateMantissa(_util, SURGE_MANTISSA, MIN_RATE, SURGE_RATE, MAX_RATE);
        // 9. Calculate the interest
        uint _interest = _totalDebt * _borrowRate * _timeDelta / (365 days * 1e18); // does the optimizer optimize this? or should it be a constant?
        // 10. Update the total debt
        _currentTotalDebt += _interest;
...........
...........        
```
As you can see code calculates utilization ratio based on `totalDebt` and `supplied` loan tokens(`_util = getUtilizationMantissa(_totalDebt, _supplied)`). `supplied` loan tokens is calculated based on current contract token balance and `totalDebt` (`_supplied = _totalDebt + _loanTokenBalance;`). after that contract use `_util` to calculate `borrowRate` by calling `getBorrowRateMantissa()`. attacker can send loan token directly to contract address and increase the contract loan token balance and it would decrease the utilization ratio and it can change the borrow rate dramatically based on `SURGE_MANTISSA, MIN_RATE, SURGE_RATE, MAX_RATE` values. imagine this scenario:
1. SURGE_RATE is 5% and SURGE_MANTISSA is 80% and MAX_RATE is 85%.
2. attacker has 100M loan from 6 month ago and totalDebt is 110M token.
3. contract state hasn't updated for 6 month(interest is not get calculated for the last 6 month).
4. current utilization is 85% (total supply is 129.4M token) which make current borrow rate to 45% (5% + (85% - 5%)*(85-80)/(100-80) = 5% + 80%/4 = 45%)
5. if attacker calls `repay()` then the calculated interest for his loan would be 100M * 45% / 2 = 22.5M (6 month interest). instead of calling `reapy()` which updates contract state, attacker would perform this actions in one transaction(by writing a smartcontract):
6. transfer 8.1M loan token to contract address and it would make total loan supply to 137.5M token (129.4M + 8.1M).
7. now the utilization ratio is 110M/137.5M = 80% and the calculated borrow rate would be 5%.
8. attacker contract would call `repay()` and code would calculate utilization as 80% (SURGE_MANTISSA ) and borrow rate as 5% (SURGE_RATE) and attacker loan interest would be 100M * 5% / 2 = 2.5M.
9. so attacker would pay 8.1M+2.5M = 10.6M instead of the 22.5M interest for the 6 month by manipulating the utilization ratio. 

the root cause of the issue is that contract uses current utilization ratio to calculate the borrow rate for that elapsed time from the last interest calculation time. current utilization ratio can be manipulated by direct fund transfer to contract address. 
in the example pool contract won't get called for the 6 month but that's not always the case. depending on the pool variables like SURGE_MANTISSA, MIN_RATE, SURGE_RATE, MAX_RATE issue can happen more often. also because performing this issue is profitable for all the borrowers they can gather together and perform it and fund the required loan token amount for the attack and attack would be more feasible.

## Impact
malicious borrower can pay lower amount of interest.

## Code Snippet
https://github.com/Surge-fi/surge-protocol-v1/blob/b7cb1dc2a2dcb4bf22c765a4222d7520843187c6/src/Pool.sol#L105-L156
https://github.com/Surge-fi/surge-protocol-v1/blob/b7cb1dc2a2dcb4bf22c765a4222d7520843187c6/src/Pool.sol#L504-L530

## Tool used
Manual Review

## Recommendation
code should either save the last contract loan balance or last contract utilization ratio and when calculating interest code should use the last time value instead of the calculating utilization with current contract balance.